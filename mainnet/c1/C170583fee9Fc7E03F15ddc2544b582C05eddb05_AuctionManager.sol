// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

import "@openzeppelin/contracts/access/Ownable.sol";
import "@openzeppelin/contracts/security/ReentrancyGuard.sol";

contract AuctionManager is Ownable, ReentrancyGuard {

    struct Auction {
        string orderId;
        string buyerId;
        uint256 editionId;
        uint256 itemId;
        uint256 price;
        address payable ownerWallet;
        address payable artistWallet;
        uint256 artistRoyalty;
    }

    uint256 public constant FEEDOMINATOR = 10000;

    address payable public vaultWallet;
    uint32 public vaultFee = 500;
    
    address public signer;
    string public salt = "\x19Ethereum Signed Message:\n32";

    event BuyAuctionItem(
        string orderId,
        string buyerId, 
        uint256 editionId, 
        uint256 itemId,
        uint256 price,
        address ownerWallet,
        address artistWallet,
        uint256 artistRoyalty
    );

    constructor(address payable _vaultWallet, address _signer) {
        require(_vaultWallet != address(0), "Invalid vault address");
        vaultWallet = _vaultWallet;
        signer = _signer;
    }

    function buyAuctionItem(
        Auction calldata auction,
        bytes calldata signature,
        uint256 deadline
    ) external payable nonReentrant {
        require(block.timestamp < deadline, "Transaction is expired");

        require(msg.value == auction.price, "Wrong price");

        {
            bytes32 messageHash = keccak256(
                abi.encodePacked(
                    auction.orderId,
                    auction.buyerId,
                    auction.editionId,
                    auction.itemId,
                    auction.ownerWallet,
                    auction.artistWallet,
                    auction.artistRoyalty,
                    deadline
                )
            );
            bytes32 ethSignedMessageHash = keccak256(abi.encodePacked(salt, messageHash));
            bool verify = recoverSigner(ethSignedMessageHash, signature) == signer;

            require(verify == true, "Buy Auction: Invalid signature");
        }
        
        {
            uint256 vault = msg.value * vaultFee / FEEDOMINATOR;

            uint256 artist = 0;

            if (auction.artistRoyalty > 0 && auction.artistWallet != address(0)) {
                artist = msg.value * auction.artistRoyalty / FEEDOMINATOR;
            }

            uint256 payout = msg.value - vault - artist;
            require(payout >= 0, "Invalid vaultFee or ArtistRoyalty");

            auction.ownerWallet.transfer(payout);

            if (auction.artistRoyalty > 0 && auction.artistWallet != address(0)) {
                auction.artistWallet.transfer(artist);
            }

            vaultWallet.transfer(vault);
        }

        emit BuyAuctionItem(
            auction.orderId,
            auction.buyerId,
            auction.editionId,
            auction.itemId,
            auction.price,
            auction.ownerWallet,
            auction.artistWallet,
            auction.artistRoyalty
        );
    }

    function recoverSigner(bytes32 _ethSignedMessageHash, bytes memory _signature)
        internal pure returns (address)
    {
        (bytes32 r, bytes32 s, uint8 v) = splitSignature(_signature);

        return ecrecover(_ethSignedMessageHash, v, r, s);
    }

    function splitSignature(bytes memory sig)
        internal pure returns (bytes32 r, bytes32 s, uint8 v)
    {
        require(sig.length == 65, "invalid signature length");

        assembly {
            /*
            First 32 bytes stores the length of the signature
            add(sig, 32) = pointer of sig + 32
            effectively, skips first 32 bytes of signature
            mload(p) loads next 32 bytes starting at the memory address p into memory
            */

            // first 32 bytes, after the length prefix
            r := mload(add(sig, 32))
            // second 32 bytes
            s := mload(add(sig, 64))
            // final byte (first byte of the next 32 bytes)
            v := byte(0, mload(add(sig, 96)))
        }

        // implicitly return (r, s, v)
    }

    /* ========== RESTRICTED FUNCTIONS ========== */
    function setVaultAddress(address payable _vaultWallet) external onlyOwner {
        require(_vaultWallet != address(0), "Invalid vault address");
        
        vaultWallet = _vaultWallet;
    }

    function setVaultFee(uint32 _fee) external onlyOwner {
        vaultFee = _fee;
    }

    function setSalt(string memory _salt) external onlyOwner {
        salt = _salt;
    }

    function setSignerAddress(address _signer) external onlyOwner {
        require(_signer != address(0), "Invalid address");
        
        signer = _signer;
    }
}

// SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;

import "../utils/Context.sol";

/**
 * @dev Contract module which provides a basic access control mechanism, where
 * there is an account (an owner) that can be granted exclusive access to
 * specific functions.
 *
 * By default, the owner account will be the one that deploys the contract. This
 * can later be changed with {transferOwnership}.
 *
 * This module is used through inheritance. It will make available the modifier
 * `onlyOwner`, which can be applied to your functions to restrict their use to
 * the owner.
 */
abstract contract Ownable is Context {
    address private _owner;

    event OwnershipTransferred(address indexed previousOwner, address indexed newOwner);

    /**
     * @dev Initializes the contract setting the deployer as the initial owner.
     */
    constructor() {
        _setOwner(_msgSender());
    }

    /**
     * @dev Returns the address of the current owner.
     */
    function owner() public view virtual returns (address) {
        return _owner;
    }

    /**
     * @dev Throws if called by any account other than the owner.
     */
    modifier onlyOwner() {
        require(owner() == _msgSender(), "Ownable: caller is not the owner");
        _;
    }

    /**
     * @dev Leaves the contract without owner. It will not be possible to call
     * `onlyOwner` functions anymore. Can only be called by the current owner.
     *
     * NOTE: Renouncing ownership will leave the contract without an owner,
     * thereby removing any functionality that is only available to the owner.
     */
    function renounceOwnership() public virtual onlyOwner {
        _setOwner(address(0));
    }

    /**
     * @dev Transfers ownership of the contract to a new account (`newOwner`).
     * Can only be called by the current owner.
     */
    function transferOwnership(address newOwner) public virtual onlyOwner {
        require(newOwner != address(0), "Ownable: new owner is the zero address");
        _setOwner(newOwner);
    }

    function _setOwner(address newOwner) private {
        address oldOwner = _owner;
        _owner = newOwner;
        emit OwnershipTransferred(oldOwner, newOwner);
    }
}

// SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;

/**
 * @dev Contract module that helps prevent reentrant calls to a function.
 *
 * Inheriting from `ReentrancyGuard` will make the {nonReentrant} modifier
 * available, which can be applied to functions to make sure there are no nested
 * (reentrant) calls to them.
 *
 * Note that because there is a single `nonReentrant` guard, functions marked as
 * `nonReentrant` may not call one another. This can be worked around by making
 * those functions `private`, and then adding `external` `nonReentrant` entry
 * points to them.
 *
 * TIP: If you would like to learn more about reentrancy and alternative ways
 * to protect against it, check out our blog post
 * https://blog.openzeppelin.com/reentrancy-after-istanbul/[Reentrancy After Istanbul].
 */
abstract contract ReentrancyGuard {
    // Booleans are more expensive than uint256 or any type that takes up a full
    // word because each write operation emits an extra SLOAD to first read the
    // slot's contents, replace the bits taken up by the boolean, and then write
    // back. This is the compiler's defense against contract upgrades and
    // pointer aliasing, and it cannot be disabled.

    // The values being non-zero value makes deployment a bit more expensive,
    // but in exchange the refund on every call to nonReentrant will be lower in
    // amount. Since refunds are capped to a percentage of the total
    // transaction's gas, it is best to keep them low in cases like this one, to
    // increase the likelihood of the full refund coming into effect.
    uint256 private constant _NOT_ENTERED = 1;
    uint256 private constant _ENTERED = 2;

    uint256 private _status;

    constructor() {
        _status = _NOT_ENTERED;
    }

    /**
     * @dev Prevents a contract from calling itself, directly or indirectly.
     * Calling a `nonReentrant` function from another `nonReentrant`
     * function is not supported. It is possible to prevent this from happening
     * by making the `nonReentrant` function external, and make it call a
     * `private` function that does the actual work.
     */
    modifier nonReentrant() {
        // On the first call to nonReentrant, _notEntered will be true
        require(_status != _ENTERED, "ReentrancyGuard: reentrant call");

        // Any calls to nonReentrant after this point will fail
        _status = _ENTERED;

        _;

        // By storing the original value once again, a refund is triggered (see
        // https://eips.ethereum.org/EIPS/eip-2200)
        _status = _NOT_ENTERED;
    }
}

// SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;

/**
 * @dev Provides information about the current execution context, including the
 * sender of the transaction and its data. While these are generally available
 * via msg.sender and msg.data, they should not be accessed in such a direct
 * manner, since when dealing with meta-transactions the account sending and
 * paying for execution may not be the actual sender (as far as an application
 * is concerned).
 *
 * This contract is only required for intermediate, library-like contracts.
 */
abstract contract Context {
    function _msgSender() internal view virtual returns (address) {
        return msg.sender;
    }

    function _msgData() internal view virtual returns (bytes calldata) {
        return msg.data;
    }
}