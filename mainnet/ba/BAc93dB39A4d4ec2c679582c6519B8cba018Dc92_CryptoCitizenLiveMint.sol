// SPDX-License-Identifier: MIT-BROUGKR
/*@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@                    @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@                             @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@                                    @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@                                         @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@                                             @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@                                                @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@                                                    @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@                                                      @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@                                                         @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@                                                           @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@                      @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@    @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@                     @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@      @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@                    @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@        @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@                   @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@          @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@                  @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@           @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@                 @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@             @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@                @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@              @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@              @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@                @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@             @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@                 @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@           @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@                  @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@         @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@                   @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@        @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@                    @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@      @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@                     @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@   @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@                      @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@                                                           @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@                                                         @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@                                                      @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@                                                    @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@                                                @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@                                             @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@                                        @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@                                   @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@                             @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@                    @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@*/
/**
 * @dev: @brougkr
 */
pragma solidity 0.8.14;
import {IERC721} from '@openzeppelin/contracts/interfaces/IERC721.sol';
import {ERC721} from '@openzeppelin/contracts/token/ERC721/ERC721.sol';
import {IERC20} from '@openzeppelin/contracts/interfaces/IERC20.sol';
import {Pausable} from '@openzeppelin/contracts/security/Pausable.sol';
import {Ownable} from '@openzeppelin/contracts/access/Ownable.sol';
import {ReentrancyGuard} from '@openzeppelin/contracts/security/ReentrancyGuard.sol';
import {MerkleProof} from '@openzeppelin/contracts/utils/cryptography/MerkleProof.sol';
import {IArtBlocks} from './IArtBlocks.sol';
import {IRandomEdition} from './IRandomEdition.sol';
contract CryptoCitizenLiveMint is Ownable, Pausable, ReentrancyGuard
{   
    /*-------------------*/
    /*  STATE VARIABLES  */
    /*-------------------*/

    bytes32 private immutable _MINTER_ROLE = keccak256("MINTER_ROLE");                                  // Minter Role
    bytes32 private immutable _DEACTIVATED_ROLE = keccak256("DELISTED_ROLE");                           // Deactivated Role
    address public immutable _BRTMULTISIG = 0xcff43A597911a9457071d89d2b2AC3D5b1862b86;                 // BRT Multisig Burn Address (mint.brightmoments.eth)
    address public _ERC20_BRT_Token = 0x9EaFE760bC0eb62f0f9c5DCa18012478d9d8B2D2;                       // BRT ERC-20 Contract Address   
    address public _ArtBlocksMintingContractCitizens = 0xDd06d8483868Cd0C5E69C24eEaA2A5F2bEaFd42b;      // ArtBlocks Minting Contract Citizens
    address public _ArtBlocksMintingContractArtists = 0x7b9a45E278b5B374bb2d96C65665d4360C97BF01;       // ArtBlocks Minting Contract Artists
    address public _ArtBlocksCoreContractCitizens = 0xbDdE08BD57e5C9fD563eE7aC61618CB2ECdc0ce0;         // ArtBlocks Citizen NFT Collection Contract Address
    address public _ArtBlocksCoreContractArtists = 0x0A1BBD57033F57E7B6743621b79fCB9Eb2CE3676;          // ArtBlocks Artist NFT Collection Contract Address 
    address public _GoldenToken = 0xC2A3c3543701009d36C0357177a62E0F6459e8A9;                           // Golden Token Contract Address
    uint public _ArtBlocksProjectID = 4;                                                                // ArtBlocks Project ID
    uint public _CurrentCityIndex;                                                                      // Current City Index
    uint _QRIndex = 333;                                                                                // QR Code Index
    bytes32 public Root = 0x6d206441d8e1510b00e84caaba4a2c7bf848f36f10a5b11d04379bc457d1124b;           // Merkle Root

    /*-------------------*/
    /*     MAPPINGS      */
    /*-------------------*/

    mapping(uint => mapping(address => bool)) public RedeemedQR;                                    // Returns If User Has BrightList Minted NFT
    mapping(uint => mapping(address => uint)) public QRAllocation;                                  // Returns User's QR Code Allocation
    mapping(uint => mapping(uint => mapping(uint => address))) public BrightListArtist;             // Returns Address Of Minting Receiver For Artist Mint 
    mapping(uint => mapping(uint => mapping(uint => uint))) public SelectedTimeSlotArtistMintPass;  // Returns Selected TimeSlot Corresponding To Artist Mint Pass
    mapping(uint => mapping(uint => mapping(uint => bool))) public DelegateStatusArtist;            // Returns Delegate Status Of Artist TicketID
    mapping(uint => mapping(uint => mapping(uint => bool))) public MintedArtist;                    // Returns Boolean If Artist MintPass ID Has Minted Or Not
    mapping(uint => mapping(uint => address)) public BrightListCitizen;                             // Returns Address Of Minting Receiver For CryptoCitizen Mint
    mapping(uint => mapping(uint => address)) public ArtistMintPasses;                              // Returns Contract Address Of Artist Mint Pass NFT 
    mapping(uint => mapping(uint => address)) public ArtistContracts;                               // Returns Contract Address Of Artist Collection NFT
    mapping(uint => mapping(uint => bool)) public MintType;                                         // Artist Mint Type (true for ArtBlocks | false for Random Edition)
    mapping(uint => mapping(uint => uint)) public BookingsAvailable;                                // Returns Amount Of Bookings Available For Input TimeSlot
    mapping(uint => mapping(uint => uint)) public SelectedTimeSlotGoldenTicket;                     // Returns Selected TimeSlot Corresponding To Golden Ticket
    mapping(uint => mapping(uint => bool)) public MintedCitizen;                                    // Returns Boolean If Golden Ticket ID Has Minted Or Not
    mapping(uint => mapping(uint => bool)) public DelegateStatusCitizen;                            // Returns Delegate Status Of Citizen TicketID
    mapping(address => bytes32) private Role;                                                       // BRT Minter Role Mapping

    /*-------------------*/
    /*      EVENTS       */
    /*-------------------*/

    /**
     * @dev Emitted When `Redeemer` IRL-mints CryptoCitizen Corresponding To Their Redeemed `TicketID`.
     **/
    event LiveMintComplete(address indexed Redeemer, uint TicketID, uint TokenID, bool Delegate);

    /**
     * @dev Emitted When `Redeemer` IRL-Mints Artist Corresponding To `ArtistID`
     */
    event LiveMintCompleteArtist(address indexed Redeemer, uint ArtistID, uint TicketID, uint TokenID, bool Delegate);

    /**
     * @dev Emitted When `Redeemer` Redeems Golden Token Corresponding To `TicketID` 
     **/
    event GoldenTokenRedeemed(address indexed Redeemer, uint TicketID, uint Slot, bool Delegate);

    /**
     * @dev Emitted When `Redeemer` Redeems Golden Token Corresponding To `TicketID` 
     **/
    event QRRedeemed(address indexed Redeemer, uint TicketID, uint Slot);

    /**
     * @dev Emitted When `Redeemer` Redeems Artist Mint Pass Corresponding To `TicketID`
     */
    event ArtistMintPassRedeemed(address indexed Redeemer, uint ArtistID, uint TicketID, uint Slot, bool Delegate);

    /**
     * @dev Emitted When `NewMinter` Is Added To BRT Minter List
     */
    event MinterAdded(address NewMinter);

    /**
     * @dev Emitted When `RemovedMinter` Is Removed From BRT Minter List
     */
    event MinterRemoved(address RemovedMinter);

    /**
     * @dev Emitted When `NewProjectID` Replaces `OldProjectID`
     */
    event ModifiedProjectID(uint OldProjectID, uint NewProjectID);

    /**
     * @dev Emitted When `NewMintingAddress` Replaces `OldMintingAddress`
     */
    event ArtBlocksMintingAddressChanged(address OldMintingAddress, address NewMintingAddress);

    /**
     * @dev Emitted When `NewCoreAddress` Replaces `OldCoreAddress` For ArtBlocks Citizen Mints
     */
    event ArtBlocksCoreAddressChanged(address OldCoreAddress, address NewCoreAddress);

    /**
     * @dev Emitted When `NewCoreAddress` Replaces `OldCoreAddress` For ArtBlocks Artist Mints
     */
    event ArtBlocksCoreAddressChangedArtist(address OldCoreAddress, address NewCoreAddress);

    /**
     * @dev Emitted When `NewGTAddress` Replaces `OldGTAddress`
     */
    event GoldenTokenAddressChanged(address OldGTAddress, address NewGTAddress);

    /**
     * @dev Emitted When `NewERC20Address` Replaces `OldERC20Address`
     */
    event ERC20AddressChanged(address OldERC20Address, address NewERC20Address);

    /**
     * @dev Emitted When `NewIndex` Replaces `OldIndex`
     */
    event CurrentCityIndexChanged(uint OldIndex, uint NewIndex);

    /**
     * @dev Emitted When Singular Timeslot Changes From `OldAmount` to `NewAmount`
     */
    event TimeSlotChanged(uint OldAmount, uint NewAmount);

    /**
     * @dev Emitted When Multiple Timeslot Changes Occur
     */
    event TimeSlotsChanged(uint[] Slots, uint[] Amounts);

    /**
     * @dev Emitted When Artist Contract Address Changes
     */
    event ArtistContractAddressChanged(address OldContractAddress, address NewContractAddress);

    /**
     * @dev Emitted When Artist Mint Type Changes
     */
    event ArtistMintTypeChanged(bool OldMintType, bool NewMintType);

    /**
     * @dev Emitted When Multisig Address Changes
     */
    event MultisigAddressChanged(address OldAddress, address NewAddress);

    /**
     * @dev Emitted When New City State Variables Are Modified 
     */
    event NewCityStarted(
        address ERC20_BRT_TokenAddress,
        address ArtBlocksMintingContractAddress,
        address ArtBlocksCoreContractAddressCitizen,
        address ArtBlocksCoreContractAddressArtist,
        address GoldenTokenAddress,
        uint ArtBlocksProjectID,
        uint CurrentCityIndex,
        uint QRIndex
    );

    /**
     * @dev Emitted When New Artists Are Seeded Into The Contract
     */
    event NewArtists(uint[] ArtistIDs, address[] MintPasses, address[] MintingAddresses, bool[] MintTypes);

    /**
     * @dev Emitted When Merkle Root Is Changed
     */
    event MerkleRootChanged(bytes32 OldRoot, bytes32 NewRoot);

    /*-------------------*/
    /*    CONSTRUCTOR    */
    /*-------------------*/

    /**
     * @dev Pre-Approves 1000 BRT For Purchasing, Grants BRT Minter Roles, & Transfers Ownership To BRT Multisig
     **/
    constructor() 
    { 
        Role[0x1A0a3E3AE390a0710f8A6d00587082273eA8F6C9] = _MINTER_ROLE; // BRT Minter #1
        Role[0x4d8013b0c264034CBf22De9DF33e22f58D52F207] = _MINTER_ROLE; // BRT Minter #2
        Role[0x4D9A8CF2fE52b8D49C7F7EAA87b2886c2bCB4160] = _MINTER_ROLE; // BRT Minter #3
        Role[0x124fd966A0D83aA020D3C54AE2c9f4800b46F460] = _MINTER_ROLE; // BRT Minter #4
        Role[0x100469feA90Ac1Fe1073E1B2b5c020A8413635c4] = _MINTER_ROLE; // BRT Minter #5
        Role[0x756De4236373fd17652b377315954ca327412bBA] = _MINTER_ROLE; // BRT Minter #6
        Role[0xc5Dfba6ef7803665C1BDE478B51Bd7eB257A2Cb9] = _MINTER_ROLE; // BRT Minter #7
        Role[0xFBF32b29Bcf8fEe32d43a4Bfd3e7249daec457C0] = _MINTER_ROLE; // BRT Minter #8
        Role[0xF2A15A83DEE7f03C70936449037d65a1C100FF27] = _MINTER_ROLE; // BRT Minter #9
        Role[0x1D2BAB965a4bB72f177Cd641C7BacF3d8257230D] = _MINTER_ROLE; // BRT Minter #10
        Role[0x2e51E8b950D72BDf003b58E357C2BA28FB77c7fB] = _MINTER_ROLE; // BRT Minter #11
        Role[0x8a7186dECb91Da854090be8226222eA42c5eeCb6] = _MINTER_ROLE; // BRT Minter #12
        BookingsAvailable[_CurrentCityIndex][20220701] = 100; 
        BookingsAvailable[_CurrentCityIndex][20220703] = 100; 
        BookingsAvailable[_CurrentCityIndex][20220706] = 100; 
        BookingsAvailable[_CurrentCityIndex][20220708] = 100; 
        BookingsAvailable[_CurrentCityIndex][20220709] = 100; 
        BookingsAvailable[_CurrentCityIndex][20220713] = 100; 
        BookingsAvailable[_CurrentCityIndex][20220715] = 100; 
        BookingsAvailable[_CurrentCityIndex][20220716] = 100; 
        BookingsAvailable[_CurrentCityIndex][20220720] = 100; 
        BookingsAvailable[_CurrentCityIndex][20220722] = 100; 
        BookingsAvailable[_CurrentCityIndex][20220723] = 100; 
        BookingsAvailable[_CurrentCityIndex][20220727] = 100; 
        BookingsAvailable[_CurrentCityIndex][20220729] = 100; 
        BookingsAvailable[_CurrentCityIndex][20220730] = 100; 
        ArtistMintPasses[_CurrentCityIndex][7] = 0x40ee4A63f6773D06F70629962AfCF52Af9AB38ed;
        ArtistMintPasses[_CurrentCityIndex][8] = 0x85885ADeD016Ba8a88593338928C88D99316ace9;
        ArtistMintPasses[_CurrentCityIndex][9] = 0x2E42CcE2b828f2176013075F37f7b64dF378fB45;
        ArtistMintPasses[_CurrentCityIndex][10] = 0xD0Ca41E0098Ffa698d9a4d33b61006a490678d94;
        ArtistMintPasses[_CurrentCityIndex][11] = 0xB22c69E6Ca346051F20207dB031810B596838138;
        ArtistMintPasses[_CurrentCityIndex][12] = 0x373E6C537F0CD423948694a93Ec1DfE5eB06eFfe;
        IERC20(_ERC20_BRT_Token).approve( // CryptoCitizens
            _ArtBlocksMintingContractCitizens, 
            0xfffffffffffffffffffffffffffffffffffffffffffffffffffffffffffffff // Maximum Approval
        );
        IERC20(0x3dF1a91Fa71c24C8c52afcE62dbA54351CBA7a63).approve( // MPLC
            _ArtBlocksMintingContractArtists, 
            0xfffffffffffffffffffffffffffffffffffffffffffffffffffffffffffffff // Maximum Approval
        );
    }

    /*-------------------*/
    /*  PUBLIC FUNCTIONS */
    /*-------------------*/

    /**
     * @dev Redeems Golden Token & BrightLists Address To Receive CryptoCitizen
     **/
    function RedeemGT(uint TicketID, uint TimeSlot, bool Delegate) public nonReentrant whenNotPaused
    {
        require(BookingsAvailable[_CurrentCityIndex][TimeSlot] > 0, "LiveMint: TimeSlot Fully Booked");
        require(IERC721(_GoldenToken).ownerOf(TicketID) == msg.sender, "ERC721: Sender Does Not Own Token With The Input Token ID");
        IERC721(_GoldenToken).transferFrom(msg.sender, _BRTMULTISIG, TicketID);
        require(IERC721(_GoldenToken).ownerOf(TicketID) == _BRTMULTISIG, "ERC721: Golden Token Redemption Failed");
        BookingsAvailable[_CurrentCityIndex][TimeSlot]--;
        SelectedTimeSlotGoldenTicket[_CurrentCityIndex][TicketID] = TimeSlot;
        BrightListCitizen[_CurrentCityIndex][TicketID] = msg.sender;
        DelegateStatusCitizen[_CurrentCityIndex][TicketID] = Delegate;
        emit GoldenTokenRedeemed(msg.sender, TicketID, TimeSlot, Delegate);
    }

    /**
     * @dev Redeems Artist Mint Pass & BrightLists Address To Receive Artist Mint
     **/
    function RedeemArtistPass(uint ArtistID, uint TicketID, uint TimeSlot, bool Delegate) public nonReentrant whenNotPaused
    {
        require(BookingsAvailable[_CurrentCityIndex][TimeSlot] > 0, "LiveMint: TimeSlot Fully Booked");
        address ArtistMintPass = resolveArtistMintPass(ArtistID);
        require(IERC721(ArtistMintPass).ownerOf(TicketID) == msg.sender, "ERC721: Sender Does Not Own Token With The Input Token ID");
        IERC721(ArtistMintPass).transferFrom(msg.sender, _BRTMULTISIG, TicketID);
        require(IERC721(ArtistMintPass).ownerOf(TicketID) == _BRTMULTISIG, "ERC721: Golden Token Redemption Failed");
        BrightListArtist[_CurrentCityIndex][ArtistID][TicketID] = msg.sender;
        SelectedTimeSlotArtistMintPass[_CurrentCityIndex][ArtistID][TicketID] = TimeSlot;
        DelegateStatusArtist[_CurrentCityIndex][ArtistID][TicketID] = Delegate;
        emit ArtistMintPassRedeemed(msg.sender, ArtistID, TicketID, TimeSlot, Delegate);
    }

    /**
     * @dev Redeems Spot For IRL Minting
     */
    function RedeemQR(uint TimeSlot, bytes32[] calldata Proof) public nonReentrant whenNotPaused
    {
        require(BookingsAvailable[_CurrentCityIndex][TimeSlot] > 0, "LiveMint: TimeSlot Fully Booked");
        require(!RedeemedQR[_CurrentCityIndex][msg.sender], "LiveMint: User Has Already Redeemed");
        require(QRAllocation[_CurrentCityIndex][msg.sender] == 0, "LiveMint: Use RedeemQRAllocation() Function");
        require(readBrightListMerkle(msg.sender, Proof), "LiveMint: User Is Not On BrightList"); 
        if(!RedeemedQR[_CurrentCityIndex][msg.sender]) { RedeemedQR[_CurrentCityIndex][msg.sender] = true; }
        BookingsAvailable[_CurrentCityIndex][TimeSlot]--;
        SelectedTimeSlotGoldenTicket[_CurrentCityIndex][_QRIndex] = TimeSlot;
        BrightListCitizen[_CurrentCityIndex][_QRIndex] = msg.sender;
        emit QRRedeemed(msg.sender, _QRIndex, TimeSlot);
        _QRIndex++;
    }

    /**
     * @dev Redeems QR Codes If User Has More Than 1 Allocation
     */
    function RedeemQRAllocation(uint TimeSlot, bytes32[] calldata Proof) public nonReentrant whenNotPaused
    {
        require(BookingsAvailable[_CurrentCityIndex][TimeSlot] > 0, "LiveMint: TimeSlot Fully Booked");
        require(QRAllocation[_CurrentCityIndex][msg.sender] > 0, "LiveMint: User Has No QR Allocation To Redeem");
        require(readBrightListMerkle(msg.sender, Proof), "LiveMint: User Is Not On BrightList"); 
        if(!RedeemedQR[_CurrentCityIndex][msg.sender]) { RedeemedQR[_CurrentCityIndex][msg.sender] = true; }
        QRAllocation[_CurrentCityIndex][msg.sender]--;
        BookingsAvailable[_CurrentCityIndex][TimeSlot]--;
        SelectedTimeSlotGoldenTicket[_CurrentCityIndex][_QRIndex] = TimeSlot;
        BrightListCitizen[_CurrentCityIndex][_QRIndex] = msg.sender;
        emit QRRedeemed(msg.sender, _QRIndex, TimeSlot);
        _QRIndex++;
    }

    /*-------------------*/
    /*     BRT STAFF     */
    /*-------------------*/

    /**
     * @dev IRL Minting Function Available Only At Bright Moments NFT Art Gallery 
     **/
    function _LiveMint(uint TicketID) public onlyMinter whenNotPaused 
    {
        address Recipient = readBrightListCitizen(TicketID);
        require(!MintedCitizen[_CurrentCityIndex][TicketID], "LiveMint: Golden Token Already Minted");
        require(Recipient != address(0), "LiveMint: Golden Token Entered Is Not Brightlisted");
        BrightListCitizen[_CurrentCityIndex][TicketID] = address(0);
        MintedCitizen[_CurrentCityIndex][TicketID] = true;
        uint TokenID = IArtBlocks(_ArtBlocksMintingContractCitizens).purchase(_ArtBlocksProjectID);
        IERC721(_ArtBlocksCoreContractCitizens).transferFrom(address(this), Recipient, TokenID);
        emit LiveMintComplete(Recipient, TicketID, TokenID, DelegateStatusCitizen[_CurrentCityIndex][TicketID]);
    }

    /**
     * @dev IRL Minting Function Available Only At Bright Moments NFT Art Gallery 
     **/
    function _LiveMintArtist(uint ArtistID, uint TicketID) public onlyMinter whenNotPaused 
    {
        address Recipient = readBrightListArtist(ArtistID, TicketID);
        require(!MintedArtist[_CurrentCityIndex][ArtistID][TicketID], "LiveMint: Artist Mint Pass Already Minted");
        require(Recipient != address(0), "LiveMint: Mint Pass Entered Is Not Brightlisted");
        BrightListArtist[_CurrentCityIndex][ArtistID][TicketID] = address(0);
        MintedArtist[_CurrentCityIndex][ArtistID][TicketID] = true;
        address ArtistMintingContractAddress = resolveArtistContract(ArtistID);
        bool ArtBlocksMint = resolveArtistMint(ArtistID);
        if(ArtBlocksMint)
        {
            uint TokenID = IArtBlocks(ArtistMintingContractAddress).purchase(ArtistID);
            IERC721(_ArtBlocksCoreContractArtists).transferFrom(address(this), Recipient, TokenID);
            emit LiveMintCompleteArtist(Recipient, ArtistID, TicketID, TokenID, DelegateStatusArtist[_CurrentCityIndex][ArtistID][TicketID]);
        }
        else 
        { 
            uint TokenID = IRandomEdition(ArtistMintingContractAddress)._Mint(Recipient, 1); 
            emit LiveMintCompleteArtist(Recipient, ArtistID, TicketID, TokenID, DelegateStatusArtist[_CurrentCityIndex][ArtistID][TicketID]);
        }
    }

    /*-------------------*/
    /*  ADMIN FUNCTIONS  */
    /*-------------------*/

    /**
     * @dev Approves BRT For Purchasing On ArtBlocks Contract
     **/
    function __ApproveERC20(address BRT, address Operator, uint Amount) external onlyOwner { IERC20(BRT).approve(Operator, Amount); }

    /**
     * @dev Batch Approves BRT For Purchasing
     */
    function __BatchApproveERC20(address[] calldata ERC20s, address[] calldata Operators, uint[] calldata Amounts) external onlyOwner
    {
        require(ERC20s.length == Operators.length && Operators.length == Amounts.length, "LiveMint: Arrays Must Be Equal Length");
        for(uint i; i < ERC20s.length; i++)
        {
            IERC20(ERC20s[i]).approve(Operators[i], Amounts[i]);
        }
    }

    /**
     * @dev Grants Address BRT Minter Role
     **/
    function __MinterAdd(address Minter) external onlyOwner 
    { 
        Role[Minter] = _MINTER_ROLE; 
        emit MinterAdded(Minter);
    }

    /**
     * @dev Deactivates Address From BRT Minter Role
     **/
    function __MinterRemove(address Minter) external onlyOwner 
    { 
        Role[Minter] = _DEACTIVATED_ROLE; 
        emit MinterRemoved(Minter);
    }

    /**
     * @dev Modifies ArtBlocks Minting Address Citizens
     */
    function __ChangeArtBlocksMintingAddress(address NewMintingAddress) external onlyOwner
    {
        address OldMintingAddress = _ArtBlocksMintingContractCitizens;
        _ArtBlocksMintingContractCitizens = NewMintingAddress;
        emit ArtBlocksMintingAddressChanged(OldMintingAddress, _ArtBlocksMintingContractCitizens);
    }

    /**
     * @dev Modifies ArtBlocks Minting Address Artists
     */
    function __ChangeArtBlocksMintingAddressArtists(address NewMintingAddress) external onlyOwner
    {
        address OldMintingAddress = _ArtBlocksMintingContractArtists;
        _ArtBlocksMintingContractArtists = NewMintingAddress;
        emit ArtBlocksMintingAddressChanged(OldMintingAddress, _ArtBlocksMintingContractArtists);
    }

    /**
     * @dev Modifies ArtBlocks Core Address Citizens
     */
    function __ChangeArtBlocksCoreAddress(address NewMintingAddress) external onlyOwner
    {
        address OldMintingAddress = _ArtBlocksCoreContractCitizens;
        _ArtBlocksCoreContractCitizens = NewMintingAddress;
        emit ArtBlocksCoreAddressChanged(OldMintingAddress, _ArtBlocksCoreContractCitizens);
    }

    /**
     * @dev Modifies ArtBlocks Core Address Artists
     */
    function __ChangeArtBlocksCoreAddressArtist(address NewMintingAddress) external onlyOwner
    {
        address OldMintingAddress = _ArtBlocksCoreContractArtists;
        _ArtBlocksCoreContractArtists = NewMintingAddress;
        emit ArtBlocksCoreAddressChangedArtist(OldMintingAddress, _ArtBlocksCoreContractArtists);
    }

    /**
     * @dev Modifies Artist Contract Addresses
     */
    function __ChangeArtistContractAddresses(uint[] calldata ArtistIDs, address[] calldata NewContractAddresses) external onlyOwner
    {
        require(ArtistIDs.length == NewContractAddresses.length, "Arrays Must Be Of Equal Length");
        for(uint ArtistID; ArtistID < ArtistIDs.length; ArtistID++)
        {
            address OldArtistContractAddress = resolveArtistContract(ArtistIDs[ArtistID]);
            ArtistContracts[_CurrentCityIndex][ArtistIDs[ArtistID]] = NewContractAddresses[ArtistID];
            emit ArtistContractAddressChanged(OldArtistContractAddress, NewContractAddresses[ArtistID]);
        }
    }

    /**
     * @dev Changes Artist Mint Passes
     */
    function __ChangeArtistMintPasses(uint[] calldata ArtistIDs, address[] calldata NewContractAddresses) external onlyOwner
    {
        require(ArtistIDs.length == NewContractAddresses.length, "Arrays Must Be Of Equal Length");
        for(uint x; x < ArtistIDs.length; x++)
        {
            address OldArtistContractAddress = ArtistMintPasses[_CurrentCityIndex][ArtistIDs[x]];
            ArtistMintPasses[_CurrentCityIndex][ArtistIDs[x]] = NewContractAddresses[x];
            emit ArtistContractAddressChanged(OldArtistContractAddress, NewContractAddresses[x]);
        }
    }

    /**
     * @dev Modifies Artist Mint Types
     * note: True For ArtBlocks | False For Random Edition
     */
    function __ChangeArtistMintTypes(uint[] calldata ArtistIDs, bool[] calldata NewMintTypes) external onlyOwner
    {
        for(uint x; x < ArtistIDs.length; x++)
        {
            bool OldMintType = resolveArtistMint(ArtistIDs[x]);
            MintType[_CurrentCityIndex][ArtistIDs[x]] = NewMintTypes[x];
            emit ArtistMintTypeChanged(OldMintType, NewMintTypes[x]);
        }
    }

    /**
     * @dev Modifies The Current ArtBlocks ProjectID
     **/
    function __ChangeArtBlocksProjectID(uint ArtBlocksProjectID) external onlyOwner 
    { 
        uint OldProjectID = _ArtBlocksProjectID;
        _ArtBlocksProjectID = ArtBlocksProjectID; 
        emit ModifiedProjectID(OldProjectID, _ArtBlocksProjectID);
    }

    /**
     * @dev Modifies Golden Token Address
     */
    function __ChangeGoldenTokenAddress(address NewGTAddress) external onlyOwner
    {
        address OldAddress = _GoldenToken;
        _GoldenToken = NewGTAddress;
        emit GoldenTokenAddressChanged(OldAddress, _GoldenToken);
    }

    /**
     * @dev Modifies ERC20 BRT Minting Token Address
     */
    function __ChangeERC20Address(address NewERC20Address) external onlyOwner
    {
        address OldAddress = _ERC20_BRT_Token;
        _ERC20_BRT_Token = NewERC20Address;
        emit ERC20AddressChanged(OldAddress, _ERC20_BRT_Token);
    }

    /**
     * @dev Modifies Current City Index
     */
    function __ChangeCityIndex(uint Index) external onlyOwner
    {
        uint OldIndex = _CurrentCityIndex;
        _CurrentCityIndex = Index;
        emit CurrentCityIndexChanged(OldIndex, _CurrentCityIndex);
    }

    /**
     * @dev Changes TimeSlot At Index `Slot` 
     * note: Slot is denoted in YYYY-DD-MM with no -'s (20220101) for Jan 1st
     */
    function __ChangeTimeSlot(uint Slot, uint Amount) external onlyOwner 
    { 
        uint OldAmount = BookingsAvailable[_CurrentCityIndex][Slot];
        BookingsAvailable[_CurrentCityIndex][Slot] = Amount; 
        emit TimeSlotChanged(OldAmount, Amount);
    }

    /**
     * @dev Changes Multiple TimeSlots 
     * note: Slot is denoted in YYYY-DD-MM with no -'s (20220101) for Jan 1st
     */
    function __ChangeTimeSlots(uint[] calldata Slots, uint[] calldata Amounts) external onlyOwner
    {
        require(Slots.length == Amounts.length, "Arrays Must Match Length");
        for(uint i; i < Slots.length; i++)
        {
            BookingsAvailable[_CurrentCityIndex][Slots[i]] = Amounts[i];
        }
        emit TimeSlotsChanged(Slots, Amounts);
    }

    /**
     * @dev Sets QR Allocation
     */
    function __ChangeQRAllocations(address[] calldata Addresses, uint[] calldata Amounts) external onlyOwner
    {
        for(uint x; x < Addresses.length; x++)
        {
            QRAllocation[_CurrentCityIndex][Addresses[x]] = Amounts[x];
        }
    }

    /**
     * @dev Sets QR Index
     */
    function __ChangeQRIndex(uint NewIndex) external onlyOwner { _QRIndex = NewIndex; }

    /**
     * @dev Batch Changes State Variables For LiveMint
     * note: __ApproveBRT(_ERC20_BRT_Token) Will Need To Be Called On This Contract After This Function
     * note: This Is So That Variables Can Be Double Checked & Mint Will Not Be Active Until That Is Complete
     */
    function __NewCity(
        address ERC20TokenAddress,
        address ArtBlocksMintingContractAddress,
        address ArtBlocksCoreContractAddress,
        address ArtBlocksCoreContractAddressArtist,
        address GoldenTokenAddress,
        uint ArtBlocksProjectID,
        uint CurrentCityIndex,
        uint QRIndex
    ) 
    external onlyOwner 
    {
        _ERC20_BRT_Token = ERC20TokenAddress;
        _ArtBlocksMintingContractCitizens = ArtBlocksMintingContractAddress;
        _ArtBlocksCoreContractCitizens = ArtBlocksCoreContractAddress;
        _ArtBlocksCoreContractArtists = ArtBlocksCoreContractAddressArtist;
        _GoldenToken = GoldenTokenAddress;
        _ArtBlocksProjectID = ArtBlocksProjectID;
        _CurrentCityIndex = CurrentCityIndex;
        _QRIndex = QRIndex;
        emit NewCityStarted(
            _ERC20_BRT_Token,
            _ArtBlocksMintingContractCitizens,
            _ArtBlocksCoreContractCitizens, 
            _ArtBlocksCoreContractArtists,
            _GoldenToken, 
            _ArtBlocksProjectID, 
            _CurrentCityIndex,
            _QRIndex
        );
    }

    /**
     * @dev Instantiates New Artists At `CurrentCityIndex` => `ArtistID[i]`
     * @dev note: ArtistIDs = uint[]
     * @dev note: MintPasses = address[] (this is the MintPass contract addresses)
     * @dev note: MintingAddresses = address[] (this is the NFTs to be Minted)
     * @dev note: MintTypes = bool[] (true for ArtBlocks) | (false for Random Edition)
     */
    function __NewArtists(
        uint[] calldata ArtistIDs, 
        address[] calldata MintPasses, 
        address[] calldata MintingAddresses,
        bool[] calldata MintTypes
    ) external onlyOwner {
        require(
            ArtistIDs.length == MintPasses.length 
            && MintPasses.length == MintingAddresses.length 
            && MintingAddresses.length == MintTypes.length, 
            "LiveMint: Array Lengths Must Be Of Equal Value"
        );
        for(uint i; i < ArtistIDs.length; i++)
        {
            ArtistMintPasses[_CurrentCityIndex][ArtistIDs[i]] = MintPasses[i];
            ArtistContracts[_CurrentCityIndex][ArtistIDs[i]] = MintingAddresses[i];
            MintType[_CurrentCityIndex][ArtistIDs[i]] = MintTypes[i];
        }
        emit NewArtists(ArtistIDs, MintPasses, MintingAddresses, MintTypes);
    }

    /**
     * @dev Changes Merkle Root
     */
    function __NewRoot(bytes32 NewRoot) external onlyOwner
    {
        bytes32 OldRoot = Root;
        Root = NewRoot;
        emit MerkleRootChanged(OldRoot, NewRoot);
    }

    /**
     * @dev Withdraws Any Ether Mistakenly Sent to Contract to Multisig
     **/
    function __WithdrawEther() external onlyOwner { payable(msg.sender).transfer(address(this).balance); }

    /**
     * @dev Withdraws ERC20 Tokens to Multisig
     **/
    function __WithdrawERC20(address tokenAddress) external onlyOwner 
    { 
        IERC20 erc20Token = IERC20(tokenAddress);
        uint balance = erc20Token.balanceOf(address(this));
        require(balance > 0, "0 ERC20 Balance At `tokenAddress`");
        erc20Token.transfer(msg.sender, balance);
    }

    /**
     * @dev Withdraws Any NFT Mistakenly Sent To This Contract.
     */
    function __WithdrawERC721(address ContractAddress, address Recipient, uint TokenID) external onlyOwner
    {
        IERC721(ContractAddress).transferFrom(address(this), Recipient, TokenID);
    }

    /*-------------------*/
    /*    PUBLIC VIEW    */
    /*-------------------*/

    /**
     * @dev Returns BrightListed Address Corresponding to Golden Ticket `TicketID`
     **/
    function readBrightListCitizen(uint TicketID) public view returns(address)
    { 
        return BrightListCitizen[_CurrentCityIndex][TicketID]; 
    }

    /**
     * @dev Returns BrightListed Address Corresponding to Mint Pass `ArtistID` & `TicketID`
     */
    function readBrightListArtist(uint ArtistID, uint TicketID) public view returns(address) 
    { 
        return BrightListArtist[_CurrentCityIndex][ArtistID][TicketID]; 
    }

    /**
     * @dev Returns Artist NFT Contract Address Correpsonding To ArtistID (1 through 10)
     */
    function resolveArtistContract(uint ArtistID) public view returns(address) 
    { 
        return ArtistContracts[_CurrentCityIndex][ArtistID]; 
    }

    /**
     * @dev Returns Artist Mint Pass Contract Address Correpsonding To ArtistID
     */
    function resolveArtistMintPass(uint ArtistID) public view returns(address) 
    { 
        return ArtistMintPasses[_CurrentCityIndex][ArtistID]; 
    }

    /**
     * @dev Batch Returns Owned Artist Mint Passes
     */
    function resolveArtistMintPasses(uint[] calldata ArtistIDs) public view returns(address[] memory)
    {
        address[] memory MintPasses = new address[](ArtistIDs.length);
        for(uint i; i < ArtistIDs.length; i++)
        {
            address x = resolveArtistMintPass(ArtistIDs[i]);
            MintPasses[i] = x;
        }
        return MintPasses;
    }

    /**
     * @dev Returns Artist ArtBlocks Mint Type (true for ArtBlocks Mint) | (false for Random Edition Mint)
     */
    function resolveArtistMint(uint ArtistID) public view returns(bool) 
    { 
        return MintType[_CurrentCityIndex][ArtistID]; 
    }

    /**
     * @dev Returns Booked TimeSlot For Golden Ticket Corresponding `TicketID`
     */
    function readTimeSlotGoldenTicket(uint TicketID) public view returns(uint) 
    { 
        return SelectedTimeSlotGoldenTicket[_CurrentCityIndex][TicketID]; 
    }

    /**
     * @dev Returns Booked TimeSlot For Artist Mint Pass Corresponding To `TicketID`
     */
    function readTimeSlotArtistMintPass(uint ArtistID, uint TicketID) public view returns(uint) 
    { 
        return SelectedTimeSlotArtistMintPass[_CurrentCityIndex][ArtistID][TicketID]; 
    }

    /**
     * @dev Returns If Golden Token Corresponding To `TicketID` Has Been Minted
     */
    function readMintedCitizen(uint TicketID) public view returns(bool)
    {
        return MintedCitizen[_CurrentCityIndex][TicketID];
    }

    /**
     * @dev Returns If Artist Mint Pass Corresponding To `TicketID` Has Been Minted
     */
    function readMintedArtist(uint ArtistID, uint TicketID) public view returns(bool)
    {
        return MintedArtist[_CurrentCityIndex][ArtistID][TicketID];
    }

    /**
     * @dev Returns Owner Of Mint Pass Corresponding To `TicketID`
     */
    function readOwnerOfMintPass(uint ArtistID, uint TicketID) public view returns(address)
    {
        return IERC721(resolveArtistMintPass(ArtistID)).ownerOf(TicketID);
    }

    /**
     * @dev Returns Owner Of Golden Token Corresponding To `TicketID`
     */
    function readOwnerOfGoldenToken(uint TicketID) public view returns(address)
    {
        return IERC721(_GoldenToken).ownerOf(TicketID);
    }

    /**
     * @dev Returns Artist Metadata
     */
    function readMetadataArtist(uint ArtistID, uint TokenID) public view returns(string memory)
    {
        if(!resolveArtistMint(ArtistID))
        {
            return IRandomEdition(resolveArtistContract(ArtistID)).tokenURI(TokenID);
        }
        else if(resolveArtistMint(ArtistID))
        {
            return IArtBlocks(resolveArtistContract(ArtistID)).tokenURI(TokenID);
        }
        else { return "Artist NFT Corresponding To `TicketID` Not Minted"; }
    }

    /**
     * @dev Returns Citizen Metadata
     */
    function readMetadataCitizen(uint TokenID) public view returns (string memory)
    {
        return IArtBlocks(_ArtBlocksCoreContractCitizens).tokenURI(TokenID);
    }

    /**
     * @dev Returns If Recipient Address Is BrightListed
     */
    function readBrightListMerkle(address Recipient, bytes32[] memory Proof) public view returns(bool)
    { 
        bytes32 Leaf = keccak256(abi.encodePacked(Recipient));
        return MerkleProof.verify(Proof, Root, Leaf);
    }

    /**
     * @dev Returns If User Is On BrightList & Has Not Redeemed QR Code
     */
    function readQREligibility(address Recipient, bytes32[] memory Proof) public view returns(bool)
    {
        bytes32 Leaf = keccak256(abi.encodePacked(Recipient));
        if(
            MerkleProof.verify(Proof, Root, Leaf) 
            && 
            !RedeemedQR[_CurrentCityIndex][Recipient]
        ) { return true; }
        else { return false; }
    }

    /**
     * @dev Returns If User Is On BrightList & Has More Than One Allocation For QR Code
     */
    function readQRAllocationEligibility(address Recipient, bytes32[] memory Proof) public view returns(bool)
    {
        bytes32 Leaf = keccak256(abi.encodePacked(Recipient));
        if(
            MerkleProof.verify(Proof, Root, Leaf) 
            && 
            QRAllocation[_CurrentCityIndex][Recipient] > 0
        ) { return true; }
        else { return false; }
    }

    /**
     * @dev Returns # Of Available Timeslots
     */
    function readTimeSlotAvailable(uint TimeSlot) public view returns(uint)
    {
        return BookingsAvailable[_CurrentCityIndex][TimeSlot];
    }

    /**
     * @dev Batch Returns If Wallet Owns Multiple TokenIDs Of Multiple NFTs
     */
    function readNFTsOwnedTokenIDs(
        address Wallet, 
        address[] calldata NFTAddresses, 
        uint Range
    ) public view returns (uint[][] memory) {
        uint[][] memory OwnedIDs = new uint[][](NFTAddresses.length);
        for(uint x; x < NFTAddresses.length; x++)
        {
            IERC721 NFT = IERC721(NFTAddresses[x]);
            uint[] memory temp = new uint[](Range);
            uint counter;
            for(uint y; y <= Range; y++)
            {
                try NFT.ownerOf(y) 
                {
                    if(NFT.ownerOf(y) == Wallet)
                    {
                        temp[counter] = y;
                        counter++;   
                    }
                } catch { }
            }
            uint[] memory FormattedOwnedIDs = new uint[](counter);
            uint index;
            for(uint z; z < counter; z++)
            {
                if(temp[z] != 0 || (z == 0 && temp[z] == 0))
                {
                    FormattedOwnedIDs[index] = temp[z];
                    index++;
                }
            }
            OwnedIDs[x] = FormattedOwnedIDs;
        }
        return OwnedIDs;
    }

    /**
     * @dev Returns Batch Metadata
     */
    function readBatchMetadata(
        address[] calldata ContractAddresses, 
        uint[][] calldata TokenIDs
    ) public view returns(string[][] memory) {
        string[][] memory Metadata = new string[][](TokenIDs.length);
        for(uint ProjectID; ProjectID < ContractAddresses.length; ProjectID++)
        {
            string[] memory ProjectMetadata = new string[](TokenIDs[ProjectID].length);
            for(uint TokenID; TokenID < TokenIDs[ProjectID].length; TokenID++)
            {
                ProjectMetadata[TokenID] = ERC721(ContractAddresses[ProjectID]).tokenURI(TokenIDs[ProjectID][TokenID]);
            }
            Metadata[ProjectID] = ProjectMetadata;
        }
        return Metadata;
    }

    /*-------------------*/
    /*      MODIFIERS    */
    /*-------------------*/

    /**
     * @dev Function Modifier That Allows Only BrightListed BRT Minters To Access
     **/
    modifier onlyMinter() 
    {
        require(Role[msg.sender] == _MINTER_ROLE, "OnlyMinter: Caller Is Not Approved BRT Minter");
        _;
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (interfaces/IERC721.sol)

pragma solidity ^0.8.0;

import "../token/ERC721/IERC721.sol";

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.5.0) (token/ERC721/ERC721.sol)

pragma solidity ^0.8.0;

import "./IERC721.sol";
import "./IERC721Receiver.sol";
import "./extensions/IERC721Metadata.sol";
import "../../utils/Address.sol";
import "../../utils/Context.sol";
import "../../utils/Strings.sol";
import "../../utils/introspection/ERC165.sol";

/**
 * @dev Implementation of https://eips.ethereum.org/EIPS/eip-721[ERC721] Non-Fungible Token Standard, including
 * the Metadata extension, but not including the Enumerable extension, which is available separately as
 * {ERC721Enumerable}.
 */
contract ERC721 is Context, ERC165, IERC721, IERC721Metadata {
    using Address for address;
    using Strings for uint256;

    // Token name
    string private _name;

    // Token symbol
    string private _symbol;

    // Mapping from token ID to owner address
    mapping(uint256 => address) private _owners;

    // Mapping owner address to token count
    mapping(address => uint256) private _balances;

    // Mapping from token ID to approved address
    mapping(uint256 => address) private _tokenApprovals;

    // Mapping from owner to operator approvals
    mapping(address => mapping(address => bool)) private _operatorApprovals;

    /**
     * @dev Initializes the contract by setting a `name` and a `symbol` to the token collection.
     */
    constructor(string memory name_, string memory symbol_) {
        _name = name_;
        _symbol = symbol_;
    }

    /**
     * @dev See {IERC165-supportsInterface}.
     */
    function supportsInterface(bytes4 interfaceId) public view virtual override(ERC165, IERC165) returns (bool) {
        return
            interfaceId == type(IERC721).interfaceId ||
            interfaceId == type(IERC721Metadata).interfaceId ||
            super.supportsInterface(interfaceId);
    }

    /**
     * @dev See {IERC721-balanceOf}.
     */
    function balanceOf(address owner) public view virtual override returns (uint256) {
        require(owner != address(0), "ERC721: balance query for the zero address");
        return _balances[owner];
    }

    /**
     * @dev See {IERC721-ownerOf}.
     */
    function ownerOf(uint256 tokenId) public view virtual override returns (address) {
        address owner = _owners[tokenId];
        require(owner != address(0), "ERC721: owner query for nonexistent token");
        return owner;
    }

    /**
     * @dev See {IERC721Metadata-name}.
     */
    function name() public view virtual override returns (string memory) {
        return _name;
    }

    /**
     * @dev See {IERC721Metadata-symbol}.
     */
    function symbol() public view virtual override returns (string memory) {
        return _symbol;
    }

    /**
     * @dev See {IERC721Metadata-tokenURI}.
     */
    function tokenURI(uint256 tokenId) public view virtual override returns (string memory) {
        require(_exists(tokenId), "ERC721Metadata: URI query for nonexistent token");

        string memory baseURI = _baseURI();
        return bytes(baseURI).length > 0 ? string(abi.encodePacked(baseURI, tokenId.toString())) : "";
    }

    /**
     * @dev Base URI for computing {tokenURI}. If set, the resulting URI for each
     * token will be the concatenation of the `baseURI` and the `tokenId`. Empty
     * by default, can be overriden in child contracts.
     */
    function _baseURI() internal view virtual returns (string memory) {
        return "";
    }

    /**
     * @dev See {IERC721-approve}.
     */
    function approve(address to, uint256 tokenId) public virtual override {
        address owner = ERC721.ownerOf(tokenId);
        require(to != owner, "ERC721: approval to current owner");

        require(
            _msgSender() == owner || isApprovedForAll(owner, _msgSender()),
            "ERC721: approve caller is not owner nor approved for all"
        );

        _approve(to, tokenId);
    }

    /**
     * @dev See {IERC721-getApproved}.
     */
    function getApproved(uint256 tokenId) public view virtual override returns (address) {
        require(_exists(tokenId), "ERC721: approved query for nonexistent token");

        return _tokenApprovals[tokenId];
    }

    /**
     * @dev See {IERC721-setApprovalForAll}.
     */
    function setApprovalForAll(address operator, bool approved) public virtual override {
        _setApprovalForAll(_msgSender(), operator, approved);
    }

    /**
     * @dev See {IERC721-isApprovedForAll}.
     */
    function isApprovedForAll(address owner, address operator) public view virtual override returns (bool) {
        return _operatorApprovals[owner][operator];
    }

    /**
     * @dev See {IERC721-transferFrom}.
     */
    function transferFrom(
        address from,
        address to,
        uint256 tokenId
    ) public virtual override {
        //solhint-disable-next-line max-line-length
        require(_isApprovedOrOwner(_msgSender(), tokenId), "ERC721: transfer caller is not owner nor approved");

        _transfer(from, to, tokenId);
    }

    /**
     * @dev See {IERC721-safeTransferFrom}.
     */
    function safeTransferFrom(
        address from,
        address to,
        uint256 tokenId
    ) public virtual override {
        safeTransferFrom(from, to, tokenId, "");
    }

    /**
     * @dev See {IERC721-safeTransferFrom}.
     */
    function safeTransferFrom(
        address from,
        address to,
        uint256 tokenId,
        bytes memory _data
    ) public virtual override {
        require(_isApprovedOrOwner(_msgSender(), tokenId), "ERC721: transfer caller is not owner nor approved");
        _safeTransfer(from, to, tokenId, _data);
    }

    /**
     * @dev Safely transfers `tokenId` token from `from` to `to`, checking first that contract recipients
     * are aware of the ERC721 protocol to prevent tokens from being forever locked.
     *
     * `_data` is additional data, it has no specified format and it is sent in call to `to`.
     *
     * This internal function is equivalent to {safeTransferFrom}, and can be used to e.g.
     * implement alternative mechanisms to perform token transfer, such as signature-based.
     *
     * Requirements:
     *
     * - `from` cannot be the zero address.
     * - `to` cannot be the zero address.
     * - `tokenId` token must exist and be owned by `from`.
     * - If `to` refers to a smart contract, it must implement {IERC721Receiver-onERC721Received}, which is called upon a safe transfer.
     *
     * Emits a {Transfer} event.
     */
    function _safeTransfer(
        address from,
        address to,
        uint256 tokenId,
        bytes memory _data
    ) internal virtual {
        _transfer(from, to, tokenId);
        require(_checkOnERC721Received(from, to, tokenId, _data), "ERC721: transfer to non ERC721Receiver implementer");
    }

    /**
     * @dev Returns whether `tokenId` exists.
     *
     * Tokens can be managed by their owner or approved accounts via {approve} or {setApprovalForAll}.
     *
     * Tokens start existing when they are minted (`_mint`),
     * and stop existing when they are burned (`_burn`).
     */
    function _exists(uint256 tokenId) internal view virtual returns (bool) {
        return _owners[tokenId] != address(0);
    }

    /**
     * @dev Returns whether `spender` is allowed to manage `tokenId`.
     *
     * Requirements:
     *
     * - `tokenId` must exist.
     */
    function _isApprovedOrOwner(address spender, uint256 tokenId) internal view virtual returns (bool) {
        require(_exists(tokenId), "ERC721: operator query for nonexistent token");
        address owner = ERC721.ownerOf(tokenId);
        return (spender == owner || getApproved(tokenId) == spender || isApprovedForAll(owner, spender));
    }

    /**
     * @dev Safely mints `tokenId` and transfers it to `to`.
     *
     * Requirements:
     *
     * - `tokenId` must not exist.
     * - If `to` refers to a smart contract, it must implement {IERC721Receiver-onERC721Received}, which is called upon a safe transfer.
     *
     * Emits a {Transfer} event.
     */
    function _safeMint(address to, uint256 tokenId) internal virtual {
        _safeMint(to, tokenId, "");
    }

    /**
     * @dev Same as {xref-ERC721-_safeMint-address-uint256-}[`_safeMint`], with an additional `data` parameter which is
     * forwarded in {IERC721Receiver-onERC721Received} to contract recipients.
     */
    function _safeMint(
        address to,
        uint256 tokenId,
        bytes memory _data
    ) internal virtual {
        _mint(to, tokenId);
        require(
            _checkOnERC721Received(address(0), to, tokenId, _data),
            "ERC721: transfer to non ERC721Receiver implementer"
        );
    }

    /**
     * @dev Mints `tokenId` and transfers it to `to`.
     *
     * WARNING: Usage of this method is discouraged, use {_safeMint} whenever possible
     *
     * Requirements:
     *
     * - `tokenId` must not exist.
     * - `to` cannot be the zero address.
     *
     * Emits a {Transfer} event.
     */
    function _mint(address to, uint256 tokenId) internal virtual {
        require(to != address(0), "ERC721: mint to the zero address");
        require(!_exists(tokenId), "ERC721: token already minted");

        _beforeTokenTransfer(address(0), to, tokenId);

        _balances[to] += 1;
        _owners[tokenId] = to;

        emit Transfer(address(0), to, tokenId);

        _afterTokenTransfer(address(0), to, tokenId);
    }

    /**
     * @dev Destroys `tokenId`.
     * The approval is cleared when the token is burned.
     *
     * Requirements:
     *
     * - `tokenId` must exist.
     *
     * Emits a {Transfer} event.
     */
    function _burn(uint256 tokenId) internal virtual {
        address owner = ERC721.ownerOf(tokenId);

        _beforeTokenTransfer(owner, address(0), tokenId);

        // Clear approvals
        _approve(address(0), tokenId);

        _balances[owner] -= 1;
        delete _owners[tokenId];

        emit Transfer(owner, address(0), tokenId);

        _afterTokenTransfer(owner, address(0), tokenId);
    }

    /**
     * @dev Transfers `tokenId` from `from` to `to`.
     *  As opposed to {transferFrom}, this imposes no restrictions on msg.sender.
     *
     * Requirements:
     *
     * - `to` cannot be the zero address.
     * - `tokenId` token must be owned by `from`.
     *
     * Emits a {Transfer} event.
     */
    function _transfer(
        address from,
        address to,
        uint256 tokenId
    ) internal virtual {
        require(ERC721.ownerOf(tokenId) == from, "ERC721: transfer from incorrect owner");
        require(to != address(0), "ERC721: transfer to the zero address");

        _beforeTokenTransfer(from, to, tokenId);

        // Clear approvals from the previous owner
        _approve(address(0), tokenId);

        _balances[from] -= 1;
        _balances[to] += 1;
        _owners[tokenId] = to;

        emit Transfer(from, to, tokenId);

        _afterTokenTransfer(from, to, tokenId);
    }

    /**
     * @dev Approve `to` to operate on `tokenId`
     *
     * Emits a {Approval} event.
     */
    function _approve(address to, uint256 tokenId) internal virtual {
        _tokenApprovals[tokenId] = to;
        emit Approval(ERC721.ownerOf(tokenId), to, tokenId);
    }

    /**
     * @dev Approve `operator` to operate on all of `owner` tokens
     *
     * Emits a {ApprovalForAll} event.
     */
    function _setApprovalForAll(
        address owner,
        address operator,
        bool approved
    ) internal virtual {
        require(owner != operator, "ERC721: approve to caller");
        _operatorApprovals[owner][operator] = approved;
        emit ApprovalForAll(owner, operator, approved);
    }

    /**
     * @dev Internal function to invoke {IERC721Receiver-onERC721Received} on a target address.
     * The call is not executed if the target address is not a contract.
     *
     * @param from address representing the previous owner of the given token ID
     * @param to target address that will receive the tokens
     * @param tokenId uint256 ID of the token to be transferred
     * @param _data bytes optional data to send along with the call
     * @return bool whether the call correctly returned the expected magic value
     */
    function _checkOnERC721Received(
        address from,
        address to,
        uint256 tokenId,
        bytes memory _data
    ) private returns (bool) {
        if (to.isContract()) {
            try IERC721Receiver(to).onERC721Received(_msgSender(), from, tokenId, _data) returns (bytes4 retval) {
                return retval == IERC721Receiver.onERC721Received.selector;
            } catch (bytes memory reason) {
                if (reason.length == 0) {
                    revert("ERC721: transfer to non ERC721Receiver implementer");
                } else {
                    assembly {
                        revert(add(32, reason), mload(reason))
                    }
                }
            }
        } else {
            return true;
        }
    }

    /**
     * @dev Hook that is called before any token transfer. This includes minting
     * and burning.
     *
     * Calling conditions:
     *
     * - When `from` and `to` are both non-zero, ``from``'s `tokenId` will be
     * transferred to `to`.
     * - When `from` is zero, `tokenId` will be minted for `to`.
     * - When `to` is zero, ``from``'s `tokenId` will be burned.
     * - `from` and `to` are never both zero.
     *
     * To learn more about hooks, head to xref:ROOT:extending-contracts.adoc#using-hooks[Using Hooks].
     */
    function _beforeTokenTransfer(
        address from,
        address to,
        uint256 tokenId
    ) internal virtual {}

    /**
     * @dev Hook that is called after any transfer of tokens. This includes
     * minting and burning.
     *
     * Calling conditions:
     *
     * - when `from` and `to` are both non-zero.
     * - `from` and `to` are never both zero.
     *
     * To learn more about hooks, head to xref:ROOT:extending-contracts.adoc#using-hooks[Using Hooks].
     */
    function _afterTokenTransfer(
        address from,
        address to,
        uint256 tokenId
    ) internal virtual {}
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (interfaces/IERC20.sol)

pragma solidity ^0.8.0;

import "../token/ERC20/IERC20.sol";

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (security/Pausable.sol)

pragma solidity ^0.8.0;

import "../utils/Context.sol";

/**
 * @dev Contract module which allows children to implement an emergency stop
 * mechanism that can be triggered by an authorized account.
 *
 * This module is used through inheritance. It will make available the
 * modifiers `whenNotPaused` and `whenPaused`, which can be applied to
 * the functions of your contract. Note that they will not be pausable by
 * simply including this module, only once the modifiers are put in place.
 */
abstract contract Pausable is Context {
    /**
     * @dev Emitted when the pause is triggered by `account`.
     */
    event Paused(address account);

    /**
     * @dev Emitted when the pause is lifted by `account`.
     */
    event Unpaused(address account);

    bool private _paused;

    /**
     * @dev Initializes the contract in unpaused state.
     */
    constructor() {
        _paused = false;
    }

    /**
     * @dev Returns true if the contract is paused, and false otherwise.
     */
    function paused() public view virtual returns (bool) {
        return _paused;
    }

    /**
     * @dev Modifier to make a function callable only when the contract is not paused.
     *
     * Requirements:
     *
     * - The contract must not be paused.
     */
    modifier whenNotPaused() {
        require(!paused(), "Pausable: paused");
        _;
    }

    /**
     * @dev Modifier to make a function callable only when the contract is paused.
     *
     * Requirements:
     *
     * - The contract must be paused.
     */
    modifier whenPaused() {
        require(paused(), "Pausable: not paused");
        _;
    }

    /**
     * @dev Triggers stopped state.
     *
     * Requirements:
     *
     * - The contract must not be paused.
     */
    function _pause() internal virtual whenNotPaused {
        _paused = true;
        emit Paused(_msgSender());
    }

    /**
     * @dev Returns to normal state.
     *
     * Requirements:
     *
     * - The contract must be paused.
     */
    function _unpause() internal virtual whenPaused {
        _paused = false;
        emit Unpaused(_msgSender());
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (access/Ownable.sol)

pragma solidity ^0.8.0;

import "../utils/Context.sol";

/**
 * @dev Contract module which provides a basic access control mechanism, where
 * there is an account (an owner) that can be granted exclusive access to
 * specific functions.
 *
 * By default, the owner account will be the one that deploys the contract. This
 * can later be changed with {transferOwnership}.
 *
 * This module is used through inheritance. It will make available the modifier
 * `onlyOwner`, which can be applied to your functions to restrict their use to
 * the owner.
 */
abstract contract Ownable is Context {
    address private _owner;

    event OwnershipTransferred(address indexed previousOwner, address indexed newOwner);

    /**
     * @dev Initializes the contract setting the deployer as the initial owner.
     */
    constructor() {
        _transferOwnership(_msgSender());
    }

    /**
     * @dev Returns the address of the current owner.
     */
    function owner() public view virtual returns (address) {
        return _owner;
    }

    /**
     * @dev Throws if called by any account other than the owner.
     */
    modifier onlyOwner() {
        require(owner() == _msgSender(), "Ownable: caller is not the owner");
        _;
    }

    /**
     * @dev Transfers ownership of the contract to a new account (`newOwner`).
     * Can only be called by the current owner.
     */
    function transferOwnership(address newOwner) public virtual onlyOwner {
        require(newOwner != address(0), "Ownable: new owner is the zero address");
        _transferOwnership(newOwner);
    }

    /**
     * @dev Transfers ownership of the contract to a new account (`newOwner`).
     * Internal function without access restriction.
     */
    function _transferOwnership(address newOwner) internal virtual {
        address oldOwner = _owner;
        _owner = newOwner;
        emit OwnershipTransferred(oldOwner, newOwner);
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (security/ReentrancyGuard.sol)

pragma solidity ^0.8.0;

/**
 * @dev Contract module that helps prevent reentrant calls to a function.
 *
 * Inheriting from `ReentrancyGuard` will make the {nonReentrant} modifier
 * available, which can be applied to functions to make sure there are no nested
 * (reentrant) calls to them.
 *
 * Note that because there is a single `nonReentrant` guard, functions marked as
 * `nonReentrant` may not call one another. This can be worked around by making
 * those functions `private`, and then adding `external` `nonReentrant` entry
 * points to them.
 *
 * TIP: If you would like to learn more about reentrancy and alternative ways
 * to protect against it, check out our blog post
 * https://blog.openzeppelin.com/reentrancy-after-istanbul/[Reentrancy After Istanbul].
 */
abstract contract ReentrancyGuard {
    // Booleans are more expensive than uint256 or any type that takes up a full
    // word because each write operation emits an extra SLOAD to first read the
    // slot's contents, replace the bits taken up by the boolean, and then write
    // back. This is the compiler's defense against contract upgrades and
    // pointer aliasing, and it cannot be disabled.

    // The values being non-zero value makes deployment a bit more expensive,
    // but in exchange the refund on every call to nonReentrant will be lower in
    // amount. Since refunds are capped to a percentage of the total
    // transaction's gas, it is best to keep them low in cases like this one, to
    // increase the likelihood of the full refund coming into effect.
    uint256 private constant _NOT_ENTERED = 1;
    uint256 private constant _ENTERED = 2;

    uint256 private _status;

    constructor() {
        _status = _NOT_ENTERED;
    }

    /**
     * @dev Prevents a contract from calling itself, directly or indirectly.
     * Calling a `nonReentrant` function from another `nonReentrant`
     * function is not supported. It is possible to prevent this from happening
     * by making the `nonReentrant` function external, and making it call a
     * `private` function that does the actual work.
     */
    modifier nonReentrant() {
        // On the first call to nonReentrant, _notEntered will be true
        require(_status != _ENTERED, "ReentrancyGuard: reentrant call");

        // Any calls to nonReentrant after this point will fail
        _status = _ENTERED;

        _;

        // By storing the original value once again, a refund is triggered (see
        // https://eips.ethereum.org/EIPS/eip-2200)
        _status = _NOT_ENTERED;
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.5.0) (utils/cryptography/MerkleProof.sol)

pragma solidity ^0.8.0;

/**
 * @dev These functions deal with verification of Merkle Trees proofs.
 *
 * The proofs can be generated using the JavaScript library
 * https://github.com/miguelmota/merkletreejs[merkletreejs].
 * Note: the hashing algorithm should be keccak256 and pair sorting should be enabled.
 *
 * See `test/utils/cryptography/MerkleProof.test.js` for some examples.
 */
library MerkleProof {
    /**
     * @dev Returns true if a `leaf` can be proved to be a part of a Merkle tree
     * defined by `root`. For this, a `proof` must be provided, containing
     * sibling hashes on the branch from the leaf to the root of the tree. Each
     * pair of leaves and each pair of pre-images are assumed to be sorted.
     */
    function verify(
        bytes32[] memory proof,
        bytes32 root,
        bytes32 leaf
    ) internal pure returns (bool) {
        return processProof(proof, leaf) == root;
    }

    /**
     * @dev Returns the rebuilt hash obtained by traversing a Merklee tree up
     * from `leaf` using `proof`. A `proof` is valid if and only if the rebuilt
     * hash matches the root of the tree. When processing the proof, the pairs
     * of leafs & pre-images are assumed to be sorted.
     *
     * _Available since v4.4._
     */
    function processProof(bytes32[] memory proof, bytes32 leaf) internal pure returns (bytes32) {
        bytes32 computedHash = leaf;
        for (uint256 i = 0; i < proof.length; i++) {
            bytes32 proofElement = proof[i];
            if (computedHash <= proofElement) {
                // Hash(current computed hash + current element of the proof)
                computedHash = _efficientHash(computedHash, proofElement);
            } else {
                // Hash(current element of the proof + current computed hash)
                computedHash = _efficientHash(proofElement, computedHash);
            }
        }
        return computedHash;
    }

    function _efficientHash(bytes32 a, bytes32 b) private pure returns (bytes32 value) {
        assembly {
            mstore(0x00, a)
            mstore(0x20, b)
            value := keccak256(0x00, 0x40)
        }
    }
}

// SPDX-License-Identifier: MIT
pragma solidity ^0.8.14;
interface IArtBlocks 
{ 
    function purchase(uint256 _projectId) payable external returns (uint tokenID); 
    function tokenURI(uint256 _tokenId) external view returns (string memory);
}

//SPDX-License-Identifier: MIT
pragma solidity 0.8.14;
interface IRandomEdition 
{ 
    function _Mint(address Recipient, uint Amount) external returns(uint tokenID); //Mints Random Edition
    function tokenURI(uint256 tokenId) external view returns (string memory); //Returns IPFS Metadata
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (token/ERC721/IERC721.sol)

pragma solidity ^0.8.0;

import "../../utils/introspection/IERC165.sol";

/**
 * @dev Required interface of an ERC721 compliant contract.
 */
interface IERC721 is IERC165 {
    /**
     * @dev Emitted when `tokenId` token is transferred from `from` to `to`.
     */
    event Transfer(address indexed from, address indexed to, uint256 indexed tokenId);

    /**
     * @dev Emitted when `owner` enables `approved` to manage the `tokenId` token.
     */
    event Approval(address indexed owner, address indexed approved, uint256 indexed tokenId);

    /**
     * @dev Emitted when `owner` enables or disables (`approved`) `operator` to manage all of its assets.
     */
    event ApprovalForAll(address indexed owner, address indexed operator, bool approved);

    /**
     * @dev Returns the number of tokens in ``owner``'s account.
     */
    function balanceOf(address owner) external view returns (uint256 balance);

    /**
     * @dev Returns the owner of the `tokenId` token.
     *
     * Requirements:
     *
     * - `tokenId` must exist.
     */
    function ownerOf(uint256 tokenId) external view returns (address owner);

    /**
     * @dev Safely transfers `tokenId` token from `from` to `to`, checking first that contract recipients
     * are aware of the ERC721 protocol to prevent tokens from being forever locked.
     *
     * Requirements:
     *
     * - `from` cannot be the zero address.
     * - `to` cannot be the zero address.
     * - `tokenId` token must exist and be owned by `from`.
     * - If the caller is not `from`, it must be have been allowed to move this token by either {approve} or {setApprovalForAll}.
     * - If `to` refers to a smart contract, it must implement {IERC721Receiver-onERC721Received}, which is called upon a safe transfer.
     *
     * Emits a {Transfer} event.
     */
    function safeTransferFrom(
        address from,
        address to,
        uint256 tokenId
    ) external;

    /**
     * @dev Transfers `tokenId` token from `from` to `to`.
     *
     * WARNING: Usage of this method is discouraged, use {safeTransferFrom} whenever possible.
     *
     * Requirements:
     *
     * - `from` cannot be the zero address.
     * - `to` cannot be the zero address.
     * - `tokenId` token must be owned by `from`.
     * - If the caller is not `from`, it must be approved to move this token by either {approve} or {setApprovalForAll}.
     *
     * Emits a {Transfer} event.
     */
    function transferFrom(
        address from,
        address to,
        uint256 tokenId
    ) external;

    /**
     * @dev Gives permission to `to` to transfer `tokenId` token to another account.
     * The approval is cleared when the token is transferred.
     *
     * Only a single account can be approved at a time, so approving the zero address clears previous approvals.
     *
     * Requirements:
     *
     * - The caller must own the token or be an approved operator.
     * - `tokenId` must exist.
     *
     * Emits an {Approval} event.
     */
    function approve(address to, uint256 tokenId) external;

    /**
     * @dev Returns the account approved for `tokenId` token.
     *
     * Requirements:
     *
     * - `tokenId` must exist.
     */
    function getApproved(uint256 tokenId) external view returns (address operator);

    /**
     * @dev Approve or remove `operator` as an operator for the caller.
     * Operators can call {transferFrom} or {safeTransferFrom} for any token owned by the caller.
     *
     * Requirements:
     *
     * - The `operator` cannot be the caller.
     *
     * Emits an {ApprovalForAll} event.
     */
    function setApprovalForAll(address operator, bool _approved) external;

    /**
     * @dev Returns if the `operator` is allowed to manage all of the assets of `owner`.
     *
     * See {setApprovalForAll}
     */
    function isApprovedForAll(address owner, address operator) external view returns (bool);

    /**
     * @dev Safely transfers `tokenId` token from `from` to `to`.
     *
     * Requirements:
     *
     * - `from` cannot be the zero address.
     * - `to` cannot be the zero address.
     * - `tokenId` token must exist and be owned by `from`.
     * - If the caller is not `from`, it must be approved to move this token by either {approve} or {setApprovalForAll}.
     * - If `to` refers to a smart contract, it must implement {IERC721Receiver-onERC721Received}, which is called upon a safe transfer.
     *
     * Emits a {Transfer} event.
     */
    function safeTransferFrom(
        address from,
        address to,
        uint256 tokenId,
        bytes calldata data
    ) external;
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (utils/introspection/IERC165.sol)

pragma solidity ^0.8.0;

/**
 * @dev Interface of the ERC165 standard, as defined in the
 * https://eips.ethereum.org/EIPS/eip-165[EIP].
 *
 * Implementers can declare support of contract interfaces, which can then be
 * queried by others ({ERC165Checker}).
 *
 * For an implementation, see {ERC165}.
 */
interface IERC165 {
    /**
     * @dev Returns true if this contract implements the interface defined by
     * `interfaceId`. See the corresponding
     * https://eips.ethereum.org/EIPS/eip-165#how-interfaces-are-identified[EIP section]
     * to learn more about how these ids are created.
     *
     * This function call must use less than 30 000 gas.
     */
    function supportsInterface(bytes4 interfaceId) external view returns (bool);
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (token/ERC721/IERC721Receiver.sol)

pragma solidity ^0.8.0;

/**
 * @title ERC721 token receiver interface
 * @dev Interface for any contract that wants to support safeTransfers
 * from ERC721 asset contracts.
 */
interface IERC721Receiver {
    /**
     * @dev Whenever an {IERC721} `tokenId` token is transferred to this contract via {IERC721-safeTransferFrom}
     * by `operator` from `from`, this function is called.
     *
     * It must return its Solidity selector to confirm the token transfer.
     * If any other value is returned or the interface is not implemented by the recipient, the transfer will be reverted.
     *
     * The selector can be obtained in Solidity with `IERC721.onERC721Received.selector`.
     */
    function onERC721Received(
        address operator,
        address from,
        uint256 tokenId,
        bytes calldata data
    ) external returns (bytes4);
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (token/ERC721/extensions/IERC721Metadata.sol)

pragma solidity ^0.8.0;

import "../IERC721.sol";

/**
 * @title ERC-721 Non-Fungible Token Standard, optional metadata extension
 * @dev See https://eips.ethereum.org/EIPS/eip-721
 */
interface IERC721Metadata is IERC721 {
    /**
     * @dev Returns the token collection name.
     */
    function name() external view returns (string memory);

    /**
     * @dev Returns the token collection symbol.
     */
    function symbol() external view returns (string memory);

    /**
     * @dev Returns the Uniform Resource Identifier (URI) for `tokenId` token.
     */
    function tokenURI(uint256 tokenId) external view returns (string memory);
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.5.0) (utils/Address.sol)

pragma solidity ^0.8.1;

/**
 * @dev Collection of functions related to the address type
 */
library Address {
    /**
     * @dev Returns true if `account` is a contract.
     *
     * [IMPORTANT]
     * ====
     * It is unsafe to assume that an address for which this function returns
     * false is an externally-owned account (EOA) and not a contract.
     *
     * Among others, `isContract` will return false for the following
     * types of addresses:
     *
     *  - an externally-owned account
     *  - a contract in construction
     *  - an address where a contract will be created
     *  - an address where a contract lived, but was destroyed
     * ====
     *
     * [IMPORTANT]
     * ====
     * You shouldn't rely on `isContract` to protect against flash loan attacks!
     *
     * Preventing calls from contracts is highly discouraged. It breaks composability, breaks support for smart wallets
     * like Gnosis Safe, and does not provide security since it can be circumvented by calling from a contract
     * constructor.
     * ====
     */
    function isContract(address account) internal view returns (bool) {
        // This method relies on extcodesize/address.code.length, which returns 0
        // for contracts in construction, since the code is only stored at the end
        // of the constructor execution.

        return account.code.length > 0;
    }

    /**
     * @dev Replacement for Solidity's `transfer`: sends `amount` wei to
     * `recipient`, forwarding all available gas and reverting on errors.
     *
     * https://eips.ethereum.org/EIPS/eip-1884[EIP1884] increases the gas cost
     * of certain opcodes, possibly making contracts go over the 2300 gas limit
     * imposed by `transfer`, making them unable to receive funds via
     * `transfer`. {sendValue} removes this limitation.
     *
     * https://diligence.consensys.net/posts/2019/09/stop-using-soliditys-transfer-now/[Learn more].
     *
     * IMPORTANT: because control is transferred to `recipient`, care must be
     * taken to not create reentrancy vulnerabilities. Consider using
     * {ReentrancyGuard} or the
     * https://solidity.readthedocs.io/en/v0.5.11/security-considerations.html#use-the-checks-effects-interactions-pattern[checks-effects-interactions pattern].
     */
    function sendValue(address payable recipient, uint256 amount) internal {
        require(address(this).balance >= amount, "Address: insufficient balance");

        (bool success, ) = recipient.call{value: amount}("");
        require(success, "Address: unable to send value, recipient may have reverted");
    }

    /**
     * @dev Performs a Solidity function call using a low level `call`. A
     * plain `call` is an unsafe replacement for a function call: use this
     * function instead.
     *
     * If `target` reverts with a revert reason, it is bubbled up by this
     * function (like regular Solidity function calls).
     *
     * Returns the raw returned data. To convert to the expected return value,
     * use https://solidity.readthedocs.io/en/latest/units-and-global-variables.html?highlight=abi.decode#abi-encoding-and-decoding-functions[`abi.decode`].
     *
     * Requirements:
     *
     * - `target` must be a contract.
     * - calling `target` with `data` must not revert.
     *
     * _Available since v3.1._
     */
    function functionCall(address target, bytes memory data) internal returns (bytes memory) {
        return functionCall(target, data, "Address: low-level call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`], but with
     * `errorMessage` as a fallback revert reason when `target` reverts.
     *
     * _Available since v3.1._
     */
    function functionCall(
        address target,
        bytes memory data,
        string memory errorMessage
    ) internal returns (bytes memory) {
        return functionCallWithValue(target, data, 0, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but also transferring `value` wei to `target`.
     *
     * Requirements:
     *
     * - the calling contract must have an ETH balance of at least `value`.
     * - the called Solidity function must be `payable`.
     *
     * _Available since v3.1._
     */
    function functionCallWithValue(
        address target,
        bytes memory data,
        uint256 value
    ) internal returns (bytes memory) {
        return functionCallWithValue(target, data, value, "Address: low-level call with value failed");
    }

    /**
     * @dev Same as {xref-Address-functionCallWithValue-address-bytes-uint256-}[`functionCallWithValue`], but
     * with `errorMessage` as a fallback revert reason when `target` reverts.
     *
     * _Available since v3.1._
     */
    function functionCallWithValue(
        address target,
        bytes memory data,
        uint256 value,
        string memory errorMessage
    ) internal returns (bytes memory) {
        require(address(this).balance >= value, "Address: insufficient balance for call");
        require(isContract(target), "Address: call to non-contract");

        (bool success, bytes memory returndata) = target.call{value: value}(data);
        return verifyCallResult(success, returndata, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but performing a static call.
     *
     * _Available since v3.3._
     */
    function functionStaticCall(address target, bytes memory data) internal view returns (bytes memory) {
        return functionStaticCall(target, data, "Address: low-level static call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-string-}[`functionCall`],
     * but performing a static call.
     *
     * _Available since v3.3._
     */
    function functionStaticCall(
        address target,
        bytes memory data,
        string memory errorMessage
    ) internal view returns (bytes memory) {
        require(isContract(target), "Address: static call to non-contract");

        (bool success, bytes memory returndata) = target.staticcall(data);
        return verifyCallResult(success, returndata, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but performing a delegate call.
     *
     * _Available since v3.4._
     */
    function functionDelegateCall(address target, bytes memory data) internal returns (bytes memory) {
        return functionDelegateCall(target, data, "Address: low-level delegate call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-string-}[`functionCall`],
     * but performing a delegate call.
     *
     * _Available since v3.4._
     */
    function functionDelegateCall(
        address target,
        bytes memory data,
        string memory errorMessage
    ) internal returns (bytes memory) {
        require(isContract(target), "Address: delegate call to non-contract");

        (bool success, bytes memory returndata) = target.delegatecall(data);
        return verifyCallResult(success, returndata, errorMessage);
    }

    /**
     * @dev Tool to verifies that a low level call was successful, and revert if it wasn't, either by bubbling the
     * revert reason using the provided one.
     *
     * _Available since v4.3._
     */
    function verifyCallResult(
        bool success,
        bytes memory returndata,
        string memory errorMessage
    ) internal pure returns (bytes memory) {
        if (success) {
            return returndata;
        } else {
            // Look for revert reason and bubble it up if present
            if (returndata.length > 0) {
                // The easiest way to bubble the revert reason is using memory via assembly

                assembly {
                    let returndata_size := mload(returndata)
                    revert(add(32, returndata), returndata_size)
                }
            } else {
                revert(errorMessage);
            }
        }
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (utils/Context.sol)

pragma solidity ^0.8.0;

/**
 * @dev Provides information about the current execution context, including the
 * sender of the transaction and its data. While these are generally available
 * via msg.sender and msg.data, they should not be accessed in such a direct
 * manner, since when dealing with meta-transactions the account sending and
 * paying for execution may not be the actual sender (as far as an application
 * is concerned).
 *
 * This contract is only required for intermediate, library-like contracts.
 */
abstract contract Context {
    function _msgSender() internal view virtual returns (address) {
        return msg.sender;
    }

    function _msgData() internal view virtual returns (bytes calldata) {
        return msg.data;
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (utils/Strings.sol)

pragma solidity ^0.8.0;

/**
 * @dev String operations.
 */
library Strings {
    bytes16 private constant _HEX_SYMBOLS = "0123456789abcdef";

    /**
     * @dev Converts a `uint256` to its ASCII `string` decimal representation.
     */
    function toString(uint256 value) internal pure returns (string memory) {
        // Inspired by OraclizeAPI's implementation - MIT licence
        // https://github.com/oraclize/ethereum-api/blob/b42146b063c7d6ee1358846c198246239e9360e8/oraclizeAPI_0.4.25.sol

        if (value == 0) {
            return "0";
        }
        uint256 temp = value;
        uint256 digits;
        while (temp != 0) {
            digits++;
            temp /= 10;
        }
        bytes memory buffer = new bytes(digits);
        while (value != 0) {
            digits -= 1;
            buffer[digits] = bytes1(uint8(48 + uint256(value % 10)));
            value /= 10;
        }
        return string(buffer);
    }

    /**
     * @dev Converts a `uint256` to its ASCII `string` hexadecimal representation.
     */
    function toHexString(uint256 value) internal pure returns (string memory) {
        if (value == 0) {
            return "0x00";
        }
        uint256 temp = value;
        uint256 length = 0;
        while (temp != 0) {
            length++;
            temp >>= 8;
        }
        return toHexString(value, length);
    }

    /**
     * @dev Converts a `uint256` to its ASCII `string` hexadecimal representation with fixed length.
     */
    function toHexString(uint256 value, uint256 length) internal pure returns (string memory) {
        bytes memory buffer = new bytes(2 * length + 2);
        buffer[0] = "0";
        buffer[1] = "x";
        for (uint256 i = 2 * length + 1; i > 1; --i) {
            buffer[i] = _HEX_SYMBOLS[value & 0xf];
            value >>= 4;
        }
        require(value == 0, "Strings: hex length insufficient");
        return string(buffer);
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (utils/introspection/ERC165.sol)

pragma solidity ^0.8.0;

import "./IERC165.sol";

/**
 * @dev Implementation of the {IERC165} interface.
 *
 * Contracts that want to implement ERC165 should inherit from this contract and override {supportsInterface} to check
 * for the additional interface id that will be supported. For example:
 *
 * ```solidity
 * function supportsInterface(bytes4 interfaceId) public view virtual override returns (bool) {
 *     return interfaceId == type(MyInterface).interfaceId || super.supportsInterface(interfaceId);
 * }
 * ```
 *
 * Alternatively, {ERC165Storage} provides an easier to use but more expensive implementation.
 */
abstract contract ERC165 is IERC165 {
    /**
     * @dev See {IERC165-supportsInterface}.
     */
    function supportsInterface(bytes4 interfaceId) public view virtual override returns (bool) {
        return interfaceId == type(IERC165).interfaceId;
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.5.0) (token/ERC20/IERC20.sol)

pragma solidity ^0.8.0;

/**
 * @dev Interface of the ERC20 standard as defined in the EIP.
 */
interface IERC20 {
    /**
     * @dev Returns the amount of tokens in existence.
     */
    function totalSupply() external view returns (uint256);

    /**
     * @dev Returns the amount of tokens owned by `account`.
     */
    function balanceOf(address account) external view returns (uint256);

    /**
     * @dev Moves `amount` tokens from the caller's account to `to`.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transfer(address to, uint256 amount) external returns (bool);

    /**
     * @dev Returns the remaining number of tokens that `spender` will be
     * allowed to spend on behalf of `owner` through {transferFrom}. This is
     * zero by default.
     *
     * This value changes when {approve} or {transferFrom} are called.
     */
    function allowance(address owner, address spender) external view returns (uint256);

    /**
     * @dev Sets `amount` as the allowance of `spender` over the caller's tokens.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * IMPORTANT: Beware that changing an allowance with this method brings the risk
     * that someone may use both the old and the new allowance by unfortunate
     * transaction ordering. One possible solution to mitigate this race
     * condition is to first reduce the spender's allowance to 0 and set the
     * desired value afterwards:
     * https://github.com/ethereum/EIPs/issues/20#issuecomment-263524729
     *
     * Emits an {Approval} event.
     */
    function approve(address spender, uint256 amount) external returns (bool);

    /**
     * @dev Moves `amount` tokens from `from` to `to` using the
     * allowance mechanism. `amount` is then deducted from the caller's
     * allowance.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transferFrom(
        address from,
        address to,
        uint256 amount
    ) external returns (bool);

    /**
     * @dev Emitted when `value` tokens are moved from one account (`from`) to
     * another (`to`).
     *
     * Note that `value` may be zero.
     */
    event Transfer(address indexed from, address indexed to, uint256 value);

    /**
     * @dev Emitted when the allowance of a `spender` for an `owner` is set by
     * a call to {approve}. `value` is the new allowance.
     */
    event Approval(address indexed owner, address indexed spender, uint256 value);
}