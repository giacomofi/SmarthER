// SPDX-License-Identifier: MIT
pragma solidity 0.8.15;

import {Base} from  "../base/Base.sol";
import {ISecurity} from "../interfaces/ISecurity.sol";
import {IToken} from "../interfaces/IToken.sol";

contract SecurityFacet is Base, ISecurity {
    /// @notice Check whether a pass has been flagged
    /// @param tokenId_ the pass's token id
    function isPassFlagged(uint256 tokenId_) public view returns (bool) {
        return s.flaggedPasses[tokenId_] != 0 || s.flaggedPassHead == tokenId_;
    }

    /// @notice Retrieve list of flagged passes
    function getFlaggedPasses() external view returns (uint256[] memory) {
        uint256[] memory flaggedPasses = new uint256[](s.flaggedPassesCount);
        uint256 currentTokenId = 0;
        for (uint256 i = 0; i < s.flaggedPassesCount; i++) {
            flaggedPasses[i] = s.flaggedPasses[currentTokenId];
            currentTokenId = s.flaggedPasses[currentTokenId];
        }
        return flaggedPasses;
    }

    /// @notice Check whether an address has been flagged
    /// @param address_ the address
    function isAddressFlagged(address address_) public view returns (bool) {
        return s.flaggedAddresses[address_] != address(0) || s.flaggedAddressHead == address_;
    }

    /// @notice Get list of flagged addresses
    function getFlaggedAddresses() external view returns (address[] memory) {
        address[] memory flaggedAddresses = new address[](s.flaggedAddressesCount);
        address currentAddress = address(0);
        for (uint256 i = 0; i < s.flaggedAddressesCount; i++) {
            flaggedAddresses[i] = s.flaggedAddresses[currentAddress];
            currentAddress = s.flaggedAddresses[currentAddress];
        }
        return flaggedAddresses;
    }

    function flagAddress(address address_) external {
        if(address_ == address(0)) revert FlagZeroAddress();
        if(isAddressFlagged(address_)) revert AddressAlreadyFlagged();
        s.flaggedAddresses[s.flaggedAddressHead] = address_;
        s.reversedFlaggedAddresses[address_] = s.flaggedAddressHead;
        s.flaggedAddressHead = address_;
        s.flaggedAddressesCount += 1;

        emit AddressFlagged(msg.sender, address_);
    }

    /// @notice used to remove the flag of an address and restore the ability for it to transfer passes
    /// @dev callable by admin or manager
    /// @param address_ the address that will be unflagged
    function unflagAddress(address address_) external {
        if(address_ == address(0)) revert FlagZeroAddress();
        if(!isAddressFlagged(address_)) revert AddressNotFlagged();
        if (address_ == s.flaggedAddressHead) {
            s.flaggedAddressHead = s.reversedFlaggedAddresses[address_];
            s.flaggedAddresses[s.flaggedAddressHead] = address(0);
        } else {
            address previousAddress = s.reversedFlaggedAddresses[address_];
            address nextAddress = s.flaggedAddresses[address_];
            s.flaggedAddresses[previousAddress] = nextAddress;
            s.reversedFlaggedAddresses[nextAddress] = previousAddress;
        }
        s.flaggedAddressesCount -= 1;

        emit AddressUnflagged(msg.sender, address_);
    }

    /// @notice used to flag a pass and make it untransferrable
    /// @dev callable by admin or manager
    /// @param tokenId_ the pass that will be flagged
    function flagPass(uint256 tokenId_) external {
        if(!s.nftStorage.burnedTokens[tokenId_] && s.nftStorage.tokenOwners[tokenId_] == address(0)) revert IToken.QueryNonExistentToken();
        if(isPassFlagged(tokenId_)) revert PassAlreadyFlagged();
        s.flaggedPasses[s.flaggedPassHead] = tokenId_;
        s.reversedFlaggedPasses[tokenId_] = s.flaggedPassHead;
        s.flaggedPassHead = tokenId_;
        s.flaggedPassesCount += 1;

        emit PassFlagged(msg.sender, tokenId_);
    }

    /// @notice used to remove the flag of a pass and restore the ability for it to be transferred
    /// @dev callable by admin or manager
    /// @param tokenId_ the pass that will be unflagged
    function unflagPass(uint256 tokenId_) external {
        if(!s.nftStorage.burnedTokens[tokenId_] && s.nftStorage.tokenOwners[tokenId_] == address(0)) revert IToken.QueryNonExistentToken();
        if(!isPassFlagged(tokenId_)) revert PassNotFlagged();
        if (tokenId_ == s.flaggedPassHead) {
            s.flaggedPassHead = s.reversedFlaggedPasses[tokenId_];
            s.flaggedPasses[s.flaggedPassHead] = 0;
        } else {
            uint256 previousPass = s.reversedFlaggedPasses[tokenId_];
            uint256 nextPass = s.flaggedPasses[tokenId_];
            s.flaggedPasses[previousPass] = nextPass;
            s.reversedFlaggedPasses[nextPass] = previousPass;
        }
        s.flaggedPassesCount -= 1;

        emit PassUnflagged(msg.sender, tokenId_);
    }

    function extraSecurityOptInLockTokens(uint256[] calldata _tokenIds) external {
        for (uint256 i = 0; i < _tokenIds.length; i++) {
            if(s.nftStorage.tokenOwners[_tokenIds[i]] != msg.sender) revert TokenNotOwnedByFromAddress();
            s.nftStorage.lockedTokens[_tokenIds[i]] = true;
        }
        emit TokensLocked(msg.sender, _tokenIds);
    }

    function unlockTokens(uint256[] calldata _tokenIds, address _owner) external {
        for (uint256 i = 0; i < _tokenIds.length; i++) {
            if(s.nftStorage.tokenOwners[_tokenIds[i]] != _owner) revert TokenNotOwnedByFromAddress();
            s.nftStorage.lockedTokens[_tokenIds[i]] = false;
        }
        emit TokensUnlocked(msg.sender, _owner, _tokenIds);
    }
}

// SPDX-License-Identifier: GPL-3.0
pragma solidity 0.8.15;

import {AppStorage} from "../libraries/LibAppStorage.sol";
import {LibDiamond} from "../libraries/LibDiamond.sol";
import {IBase} from "../interfaces/IBase.sol";
import {ISecurity} from "../interfaces/ISecurity.sol";


abstract contract Base is IBase {
    AppStorage internal s;
    uint256 private constant MAX_UINT = type(uint256).max;

    modifier onlyEoA() {
         // solhint-disable-next-line avoid-tx-origin
        if(tx.origin != msg.sender) revert CallerNotEoA();
        _;
    }

    modifier isTransferable() {
        if(!s.nftStorage.transfersEnabled) revert TransfersPaused();
        _;
    }

    modifier tokenLocked(uint256 tokenId) {
        if(s.nftStorage.lockedTokens[tokenId]) revert ISecurity.TokenLocked();
        _;
    }

    /// @notice check that a String is NOT NULL
    /// @dev Explain to a developer any extra details
    /// @param string_ the string to check
    modifier isEmpty(string calldata string_) {
        if (bytes(string_).length == 0 ) revert EmptyString();
        _;
    }
}

// SPDX-License-Identifier: MIT
pragma solidity 0.8.15;

interface ISecurity {
    event PassFlagged(address indexed sender, uint256 tokenId);
    event PassUnflagged(address indexed sender, uint256 tokenId);
    event AddressFlagged(address indexed sender, address flaggedAddress);
    event AddressUnflagged(address indexed sender, address unflaggedAddress);
    event PassBurned(address indexed sender, uint256 tokenId);

    error TokenNotOwnedByFromAddress();
    error TokenLocked();
    error FlagZeroAddress();
    error AddressAlreadyFlagged();
    error AddressNotFlagged();
    error PassAlreadyFlagged();
    error PassNotFlagged();
    event TokensLocked(address indexed owner, uint256[] tokenIds);
    event TokensUnlocked(address indexed sender, address indexed owner, uint256[] tokenIds);
}

// SPDX-License-Identifier: MIT
pragma solidity 0.8.15;

interface IToken {
    error QueryNonExistentToken();
}

// SPDX-License-Identifier: GPL-3.0
pragma solidity 0.8.15;

import {LibNFTStorage} from "./LibNFTStorage.sol";
import {LibRoyaltyStorage} from "./LibRoyaltyStorage.sol";

struct AppStorage {
    LibNFTStorage nftStorage;
    LibRoyaltyStorage royaltyStorage;
    mapping(uint256 => uint256) flaggedPasses;
    mapping(uint256 => uint256) reversedFlaggedPasses;
    uint256  flaggedPassHead;
    mapping(address => address)  flaggedAddresses;
    mapping(address => address)  reversedFlaggedAddresses;
    address  flaggedAddressHead;
    bool  isRefundingGas;
    uint256  maxRefundAmount;
    uint256  refundGasBuffer;
    uint256  flaggedAddressesCount;
    uint256  flaggedPassesCount;
    mapping(address => bool) adminTransferUsers;
}

// SPDX-License-Identifier: MIT
pragma solidity 0.8.15;

/******************************************************************************\
* Author: Nick Mudge <[email protected]> (https://twitter.com/mudgen)
* EIP-2535 Diamond Standard: https://eips.ethereum.org/EIPS/eip-2535
/******************************************************************************/

import {IDiamondCut} from "../interfaces/IDiamondCut.sol";
import {IDiamondLoupe} from "../interfaces/IDiamondLoupe.sol";
import {IERC165} from "../interfaces/IERC165.sol";
import {IERC173} from "../interfaces/IERC173.sol";
import {LibMeta} from "./LibMeta.sol";

library LibDiamond {
    bytes32 constant DIAMOND_STORAGE_POSITION = keccak256("diamond.standard.diamond.storage");

    struct FacetAddressAndPosition {
        address facetAddress;
        uint16 functionSelectorPosition; // position in facetFunctionSelectors.functionSelectors array

    }

    struct FacetFunctionSelectors {
        bytes4[] functionSelectors;
        uint16 facetAddressPosition; // position of facetAddress in facetAddresses array
    }

    struct DiamondStorage {
        // maps function selector to the facet address and
        // the position of the selector in the facetFunctionSelectors.selectors array
        mapping(bytes4 => FacetAddressAndPosition) selectorToFacetAndPosition;
        // maps facet addresses to function selectors
        mapping(address => FacetFunctionSelectors) facetFunctionSelectors;
        // maps function selector to role based access control
        mapping(bytes4 => bytes32[]) selectorToAccessControl;
        // facet addresses
        address[] facetAddresses;
        // Used to query if a contract implements an interface.
        // Used to implement ERC-165.
        mapping(bytes4 => bool) supportedInterfaces;
        // Owner of the contract
        address contractOwner;
    }

    function diamondStorage() internal pure returns (DiamondStorage storage ds) {
        bytes32 position = DIAMOND_STORAGE_POSITION;
        assembly {
            ds.slot := position
        }
    }

    event OwnershipTransferred(address indexed previousOwner, address indexed newOwner);

    function setContractOwner(address _newOwner) internal {
        DiamondStorage storage ds = diamondStorage();
        address previousOwner = ds.contractOwner;
        ds.contractOwner = _newOwner;
        emit OwnershipTransferred(previousOwner, _newOwner);
    }

    function contractOwner() internal view returns (address contractOwner_) {
        contractOwner_ = diamondStorage().contractOwner;
    }

    function enforceIsContractOwner() internal view {
        require(LibMeta.msgSender() == diamondStorage().contractOwner, "LibDiamond: Must be contract owner");
    }

    event DiamondCut(IDiamondCut.FacetCut[] _diamondCut, address _init, bytes _calldata);

    function addDiamondFunctions(
        address _diamondCutFacet,
        address _diamondLoupeFacet,
        address _ownershipFacet
    ) internal {
        IDiamondCut.FacetCut[] memory cut = new IDiamondCut.FacetCut[](3);
        IDiamondCut.FunctionSelector[] memory functionSelectors = new IDiamondCut.FunctionSelector[](1);
        bytes32[] memory roles = new bytes32[](0);
        functionSelectors[0] = IDiamondCut.FunctionSelector(IDiamondCut.diamondCut.selector, roles);
        cut[0] = IDiamondCut.FacetCut({facetAddress: _diamondCutFacet, action: IDiamondCut.FacetCutAction.Add, functionSelectors: functionSelectors});
        functionSelectors = new IDiamondCut.FunctionSelector[](5);
        functionSelectors[0] = IDiamondCut.FunctionSelector(IDiamondLoupe.facets.selector, roles);
        functionSelectors[1] = IDiamondCut.FunctionSelector(IDiamondLoupe.facetFunctionSelectors.selector, roles);
        functionSelectors[2] = IDiamondCut.FunctionSelector(IDiamondLoupe.facetAddresses.selector, roles);
        functionSelectors[3] = IDiamondCut.FunctionSelector(IDiamondLoupe.facetAddress.selector, roles);
        functionSelectors[4] = IDiamondCut.FunctionSelector(IERC165.supportsInterface.selector, roles);
        cut[1] = IDiamondCut.FacetCut({
            facetAddress: _diamondLoupeFacet,
            action: IDiamondCut.FacetCutAction.Add,
            functionSelectors: functionSelectors
        });
        functionSelectors = new IDiamondCut.FunctionSelector[](2);
        functionSelectors[0] = IDiamondCut.FunctionSelector(IERC173.transferOwnership.selector, roles);
        functionSelectors[1] = IDiamondCut.FunctionSelector(IERC173.owner.selector, roles);
        cut[2] = IDiamondCut.FacetCut({facetAddress: _ownershipFacet, action: IDiamondCut.FacetCutAction.Add, functionSelectors: functionSelectors});
        diamondCut(cut, address(0), "");
    }

    // Internal function version of diamondCut
    function diamondCut(
        IDiamondCut.FacetCut[] memory _diamondCut,
        address _init,
        bytes memory _calldata
    ) internal {
        for (uint256 facetIndex; facetIndex < _diamondCut.length; facetIndex++) {
            IDiamondCut.FacetCutAction action = _diamondCut[facetIndex].action;
            if (action == IDiamondCut.FacetCutAction.Add) {
                addFunctions(_diamondCut[facetIndex].facetAddress, _diamondCut[facetIndex].functionSelectors);
            } else if (action == IDiamondCut.FacetCutAction.Replace) {
                replaceFunctions(_diamondCut[facetIndex].facetAddress, _diamondCut[facetIndex].functionSelectors);
            } else if (action == IDiamondCut.FacetCutAction.Remove) {
                removeFunctions(_diamondCut[facetIndex].facetAddress, _diamondCut[facetIndex].functionSelectors);
            } else {
                revert("LibDiamondCut: Incorrect FacetCutAction");
            }
        }
        emit DiamondCut(_diamondCut, _init, _calldata);
        initializeDiamondCut(_init, _calldata);
    }

    function addFunctions(address _facetAddress, IDiamondCut.FunctionSelector[] memory _functionSelectors) internal {
        require(_functionSelectors.length > 0, "LibDiamondCut: No selectors in facet to cut");
        DiamondStorage storage ds = diamondStorage();
        // uint16 selectorCount = uint16(diamondStorage().selectors.length);
        require(_facetAddress != address(0), "LibDiamondCut: Add facet can't be address(0)");
        uint16 selectorPosition = uint16(ds.facetFunctionSelectors[_facetAddress].functionSelectors.length);
        // add new facet address if it does not exist
        if (selectorPosition == 0) {
            enforceHasContractCode(_facetAddress, "LibDiamondCut: New facet has no code");
            ds.facetFunctionSelectors[_facetAddress].facetAddressPosition = uint16(ds.facetAddresses.length);
            ds.facetAddresses.push(_facetAddress);
        }
        for (uint256 selectorIndex; selectorIndex < _functionSelectors.length; selectorIndex++) {
            bytes4 selector = _functionSelectors[selectorIndex].selector;
            address oldFacetAddress = ds.selectorToFacetAndPosition[selector].facetAddress;
            require(oldFacetAddress == address(0), "LibDiamondCut: Can't add function that already exists");
            ds.facetFunctionSelectors[_facetAddress].functionSelectors.push(selector);
            ds.selectorToFacetAndPosition[selector].facetAddress = _facetAddress;
            ds.selectorToFacetAndPosition[selector].functionSelectorPosition = selectorPosition;
            ds.selectorToAccessControl[selector] = _functionSelectors[selectorIndex].roles;
            selectorPosition++;
        }
    }

    function replaceFunctions(address _facetAddress, IDiamondCut.FunctionSelector[] memory _functionSelectors) internal {
        require(_functionSelectors.length > 0, "LibDiamondCut: No selectors in facet to cut");
        DiamondStorage storage ds = diamondStorage();
        require(_facetAddress != address(0), "LibDiamondCut: Add facet can't be address(0)");
        uint16 selectorPosition = uint16(ds.facetFunctionSelectors[_facetAddress].functionSelectors.length);
        // add new facet address if it does not exist
        if (selectorPosition == 0) {
            enforceHasContractCode(_facetAddress, "LibDiamondCut: New facet has no code");
            ds.facetFunctionSelectors[_facetAddress].facetAddressPosition = uint16(ds.facetAddresses.length);
            ds.facetAddresses.push(_facetAddress);
        }
        for (uint256 selectorIndex; selectorIndex < _functionSelectors.length; selectorIndex++) {
            bytes4 selector = _functionSelectors[selectorIndex].selector;
            address oldFacetAddress = ds.selectorToFacetAndPosition[selector].facetAddress;
            require(oldFacetAddress != _facetAddress, "LibDiamondCut: Can't replace function with same function");
            removeFunction(oldFacetAddress, selector);
            // add function
            ds.selectorToFacetAndPosition[selector].functionSelectorPosition = selectorPosition;
            ds.facetFunctionSelectors[_facetAddress].functionSelectors.push(selector);
            ds.selectorToFacetAndPosition[selector].facetAddress = _facetAddress;
            ds.selectorToAccessControl[selector] = _functionSelectors[selectorIndex].roles;
            selectorPosition++;
        }
    }

    function removeFunctions(address _facetAddress, IDiamondCut.FunctionSelector[] memory _functionSelectors) internal {
        require(_functionSelectors.length > 0, "LibDiamondCut: No selectors in facet to cut");
        DiamondStorage storage ds = diamondStorage();
        // if function does not exist then do nothing and return
        require(_facetAddress == address(0), "LibDiamondCut: Remove facet address must be address(0)");
        for (uint256 selectorIndex; selectorIndex < _functionSelectors.length; selectorIndex++) {
            bytes4 selector = _functionSelectors[selectorIndex].selector;
            address oldFacetAddress = ds.selectorToFacetAndPosition[selector].facetAddress;
            removeFunction(oldFacetAddress, selector);
        }
    }

    function removeFunction(address _facetAddress, bytes4 _selector) internal {
        DiamondStorage storage ds = diamondStorage();
        require(_facetAddress != address(0), "LibDiamondCut: Can't remove function that doesn't exist");
        // an immutable function is a function defined directly in a diamond
        require(_facetAddress != address(this), "LibDiamondCut: Can't remove immutable function");
        // replace selector with last selector, then delete last selector
        uint256 selectorPosition = ds.selectorToFacetAndPosition[_selector].functionSelectorPosition;
        uint256 lastSelectorPosition = ds.facetFunctionSelectors[_facetAddress].functionSelectors.length - 1;
        // if not the same then replace _selector with lastSelector
        if (selectorPosition != lastSelectorPosition) {
            bytes4 lastSelector = ds.facetFunctionSelectors[_facetAddress].functionSelectors[lastSelectorPosition];
            ds.facetFunctionSelectors[_facetAddress].functionSelectors[selectorPosition] = lastSelector;
            ds.selectorToFacetAndPosition[lastSelector].functionSelectorPosition = uint16(selectorPosition);
        }
        // delete the last selector
        ds.facetFunctionSelectors[_facetAddress].functionSelectors.pop();
        delete ds.selectorToFacetAndPosition[_selector];

        // if no more selectors for facet address then delete the facet address
        if (lastSelectorPosition == 0) {
            // replace facet address with last facet address and delete last facet address
            uint256 lastFacetAddressPosition = ds.facetAddresses.length - 1;
            uint256 facetAddressPosition = ds.facetFunctionSelectors[_facetAddress].facetAddressPosition;
            if (facetAddressPosition != lastFacetAddressPosition) {
                address lastFacetAddress = ds.facetAddresses[lastFacetAddressPosition];
                ds.facetAddresses[facetAddressPosition] = lastFacetAddress;
                ds.facetFunctionSelectors[lastFacetAddress].facetAddressPosition = uint16(facetAddressPosition);
            }
            ds.facetAddresses.pop();
            delete ds.facetFunctionSelectors[_facetAddress].facetAddressPosition;
        }

        // delete role based access control of selector
        delete ds.selectorToAccessControl[_selector];
    }

    function initializeDiamondCut(address _init, bytes memory _calldata) internal {
        if (_init == address(0)) {
            require(_calldata.length == 0, "LibDiamondCut: _init is address(0) but_calldata is not empty");
        } else {
            require(_calldata.length > 0, "LibDiamondCut: _calldata is empty but _init is not address(0)");
            if (_init != address(this)) {
                enforceHasContractCode(_init, "LibDiamondCut: _init address has no code");
            }
            (bool success, bytes memory error) = _init.delegatecall(_calldata);
            if (success == false) {
                if (error.length > 0) {
                    // bubble up the error
                    revert(string(error));
                } else {
                    revert("LibDiamondCut: _init function reverted");
                }
            }
        }
    }

    function enforceHasContractCode(address _contract, string memory _errorMessage) internal view {
        uint256 contractSize;
        assembly {
            contractSize := extcodesize(_contract)
        }
        require(contractSize != 0, _errorMessage);
    }
}

// SPDX-License-Identifier: MIT
pragma solidity 0.8.15;

interface IBase {
    error TransfersPaused();
    error CallerNotEoA();
    error EmptyString();
    /**
        RBAC
    */
    error CallerNotAuthorized();
    event TransfersLockedChanged(address indexed sender, bool locked);
}

// SPDX-License-Identifier: MIT
pragma solidity 0.8.15;

struct LibNFTStorage {
    string name;
    string symbol;
    string baseURI;
    string contractURI;

    uint256 nextTokenId;
    uint256 startingTokenId;
    uint256 burnCounter;

    bool initialized;
    bool transfersEnabled;

    mapping(uint256 => bool) lockedTokens;
    mapping(uint256 => address) tokenOwners;
    mapping(uint256 => address) tokenOperators;
    mapping(uint256 => bool) burnedTokens;
    mapping(address => uint256) balances;
    mapping(address => mapping(address => bool)) operators;
}

// SPDX-License-Identifier: MIT
pragma solidity 0.8.15;

struct LibRoyaltyStorage {
    uint256 denominator;
    uint256 numerator;
    address receiver;
    mapping(uint256 => uint256) tokenNumerators;
}

// SPDX-License-Identifier: MIT
pragma solidity 0.8.15;

/******************************************************************************\
* Author: Nick Mudge <[email protected]> (https://twitter.com/mudgen)
/******************************************************************************/

interface IDiamondCut {
    enum FacetCutAction {Add, Replace, Remove}

    struct FunctionSelector {
        bytes4 selector;
        bytes32[] roles;
    }

    struct FacetCut {
        address facetAddress;
        FacetCutAction action;
        FunctionSelector[] functionSelectors;
    }

    /// @notice Add/replace/remove any number of functions and optionally execute
    ///         a function with delegatecall
    /// @param _diamondCut Contains the facet addresses and function selectors
    /// @param _init The address of the contract or facet to execute _calldata
    /// @param _calldata A function call, including function selector and arguments
    ///                  _calldata is executed with delegatecall on _init
    function diamondCut(
        FacetCut[] calldata _diamondCut,
        address _init,
        bytes calldata _calldata
    ) external;

    event DiamondCut(FacetCut[] _diamondCut, address _init, bytes _calldata);
}

// SPDX-License-Identifier: MIT
pragma solidity 0.8.15;

// A loupe is a small magnifying glass used to look at diamonds.
// These functions look at diamonds
interface IDiamondLoupe {
    /// These functions are expected to be called frequently
    /// by tools.

    struct Facet {
        address facetAddress;
        bytes4[] functionSelectors;
    }

    /// @notice Gets all facet addresses and their four byte function selectors.
    /// @return facets_ Facet
    function facets() external view returns (Facet[] memory facets_);

    /// @notice Gets all the function selectors supported by a specific facet.
    /// @param _facet The facet address.
    /// @return facetFunctionSelectors_
    function facetFunctionSelectors(address _facet) external view returns (bytes4[] memory facetFunctionSelectors_);

    /// @notice Get all the facet addresses used by a diamond.
    /// @return facetAddresses_
    function facetAddresses() external view returns (address[] memory facetAddresses_);

    /// @notice Gets the facet that supports the given selector.
    /// @dev If facet is not found return address(0).
    /// @param _functionSelector The function selector.
    /// @return facetAddress_ The facet address.
    function facetAddress(bytes4 _functionSelector) external view returns (address facetAddress_);
}

// SPDX-License-Identifier: MIT
pragma solidity 0.8.15;

interface IERC165 {
    /// @notice Query if a contract implements an interface
    /// @param interfaceId The interface identifier, as specified in ERC-165
    /// @dev Interface identification is specified in ERC-165. This function
    ///  uses less than 30,000 gas.
    /// @return `true` if the contract implements `interfaceID` and
    ///  `interfaceID` is not 0xffffffff, `false` otherwise
    function supportsInterface(bytes4 interfaceId) external view returns (bool);
}

// SPDX-License-Identifier: MIT
pragma solidity 0.8.15;

/// @title ERC-173 Contract Ownership Standard
///  Note: the ERC-165 identifier for this interface is 0x7f5828d0
/* is ERC165 */
interface IERC173 {
    /// @notice Get the address of the owner
    /// @return owner_ The address of the owner.
    function owner() external view returns (address owner_);

    /// @notice Set the address of the new owner of the contract
    /// @dev Set _newOwner to address(0) to renounce any ownership.
    /// @param _newOwner The address of the new owner of the contract
    function transferOwnership(address _newOwner) external;
}

// SPDX-License-Identifier: MIT
pragma solidity 0.8.15;

library LibMeta {
    bytes32 internal constant EIP712_DOMAIN_TYPEHASH =
        keccak256(bytes("EIP712Domain(string name,string version,uint256 salt,address verifyingContract)"));

    function domainSeparator(string memory name, string memory version) internal view returns (bytes32 domainSeparator_) {
        domainSeparator_ = keccak256(
            abi.encode(EIP712_DOMAIN_TYPEHASH, keccak256(bytes(name)), keccak256(bytes(version)), getChainID(), address(this))
        );
    }

    function getChainID() internal view returns (uint256 id) {
        assembly {
            id := chainid()
        }
    }

    function msgSender() internal view returns (address sender_) {
        if (msg.sender == address(this)) {
            bytes memory array = msg.data;
            uint256 index = msg.data.length;
            assembly {
                // Load the 32 bytes word from memory with the address on the lower 20 bytes, and mask those.
                sender_ := and(mload(add(array, index)), 0xffffffffffffffffffffffffffffffffffffffff)
            }
        } else {
            sender_ = msg.sender;
        }
    }
}